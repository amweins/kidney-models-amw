	PROGRAM BCR
C
C   IT IS ASSUMED THAT THE SOLUTION IS MADE UP VIA HCL TITRATION:
C   I.E. FIRST THE HCO3 CONCENTRATION IS SPECIFIED ALONG WITH THE TOTAL
C   CONCENTRATION OF THE OTHER BUFFERS; NEXT THE BUFFER PAIRS ARE 
C   ADJUSTED; AND FINALLY THE CL CONCENTRATION IS CALCULATED FROM 
C   ELECTRONEUTRALITY.
C
	INTEGER SOLS,NTUB
C
	DOUBLE PRECISION
     1   Z(12),ZIMPM,
     1   PKC,PKP,PKN,KCO2,
     1   TPM(10),TNM(10),QPM(10),QNM(10)
C LUMINAL VARIABLES
	DOUBLE PRECISION
     1   CM(15,10),IMPM(10),LCHM(10),
     1   FVM(10),FKM(16,10)
C
	OPEN (19,FILE='bound.tem')
	OPEN (22,FILE='bound.dat')
C
	SOLS=12
        NTUB=6
C
C  SOLUTE INDEX:
C	1-	NA+
C	2-	K+
C	3-	CL-
C	4-	HCO3-
C	5-	H2CO3
C	6-	CO2
C	7-	HPO4--
C	8-	H2PO4-
C	9-	UREA
C	10-	NH3
C	11-	NH4+
C	12-	H+
C
	Z(1)=1.D0
	Z(2)=1.D0
	Z(3)=-1.D0
	Z(4)=-1.D0
	Z(5)=0.D0
	Z(6)=0.D0
	Z(7)=-2.D0
	Z(8)=-1.D0
	Z(9)=0.D0
	Z(10)=0.D0
	Z(11)=1.D0
	Z(12)=1.D0
	ZIMPM=1.D0
C
	PKC=3.57
	PKP=6.8
	PKN=9.15
	KCO2=340.
C
C BOUNDARY VALUES,ACTIVE TRANSPORT,AND THE TIME INCREMENT ARE
C READ. THE TIME STEP WILL CYCLE FROM THIS POINT.
C
	READ(19,50) 
     1   (FVM(J),J=1,NTUB),
     1   (IMPM(J),J=1,NTUB),
     1   ((CM(I,J),J=1,NTUB),I=1,SOLS-1)
   50   FORMAT (6D14.5,/,(6F14.9))
C
C ASSUMING EQUILIBRIUM BETWEEN DISSOLVED CO2 AND H2CO3
C
        DO 140 J=1,NTUB
	CM(5,J)=CM(6,J)/KCO2
	LCHM(J)=PKC + DLOG10(CM(4,J)/CM(5,J))
	CM(12,J)=10**(-LCHM(J))
C
	TPM(J)=CM(7,J)+CM(8,J)
	TNM(J)=CM(10,J)+CM(11,J)
C
	QPM(J)=10.**(LCHM(J)-PKP)
	QNM(J)=10.**(LCHM(J)-PKN)
C
	CM(8,J)=TPM(J)/(1.+QPM(J))
	CM(7,J)=TPM(J)-CM(8,J)
	CM(11,J)=TNM(J)/(1.+QNM(J))
	CM(10,J)=TNM(J)-CM(11,J)
C
	CM(3,J)=IMPM(J)*ZIMPM + CM(1,J)*Z(1) + CM(2,J)*Z(2)
	DO 130 I=4,SOLS
	CM(3,J)=CM(3,J)+CM(I,J)*Z(I)
  130   CONTINUE
  140   CONTINUE
C
	WRITE(22,150) 
     1   (FVM(J),J=1,NTUB),
     1   (IMPM(J),J=1,NTUB),
     1   ((CM(I,J),J=1,NTUB),I=1,SOLS-1)
  150   FORMAT (6D14.5,/,(6F14.9))
C
  900   CONTINUE
	STOP
	END
