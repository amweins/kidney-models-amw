	SUBROUTINE EQLIB
C
C TAKES THE FINAL IMCT URINE AND COMPUTES THE FINAL PH, CO2, 
C  AND BUFFER CONCENTRATIONS AFTER EQUILIBRATION IN A CLOSED SYSTEM
C
	INTEGER SOLS,T,X
C
C GENERAL PARAMETERS
	DOUBLE PRECISION
     1   Z(12),RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,DIST,
     1   PKC,PKP,PKN,KHY(5),KDHY(5),L0,L(1601,2)
C LUMINAL AND PERITUBULAR PARAMETERS
	DOUBLE PRECISION
     1   VM(1601,2),PM(1601,2),CM(12,1601,2),
     1   IMPM(1601,2),LCHM(1601),XM(12,1601),
     1   VS(1601,2),PS(1601,2),CS(12,1601,2),
     1   IMPS(1601,2),LCHS(1601),XS(12,1601),ZIMPS,
     1   TL,DX,RM0,MUM,ETA,
     1   SM(1601,2),AM(1601,2),FVM(1601,2),FKM(13,1601,2)
C INTERSPACE PARAMETERS
	DOUBLE PRECISION
     1   AME,AE0,AE(1601,2),MUA,CHVL0,CHVL(1601,2),MUV,
     1   LPME,LPES,SME(12),SES(12),
     1   HME(12),HES(12),CME(12),CES(12),
     1   VE(1601,2),PE(1601,2),CE(12,1601,2),LCHE(1601),XE(12,1601),
     1   FEVM(1601,2),FEKM(12,1601,2),FEVS(1601,2),FEKS(12,1601,2),
     1   CURE(1601)
C CELL PARAMETERS
	DOUBLE PRECISION
     1   AIE(3),AI0(3),CLVL0(3),IMP0(3),CLVL(3,1601,2),
     1   ZIMP(3),TBUF(3),PKB(3),CBUF(3,1601,2),HCBUF(3,1601,2),
     1   LPMI(3),LPIS(3),SMI(3,12),SIS(3,12),
     1   HMI(3,12),HIS(3,12),CMI(3,12),CIE(3,12),CIS(3,12),
     1   LMI(3,12,12),LIS(3,12,12),ATMI(3,12,1601),ATIS(3,12,1601),
     1   VI(3,1601,2),PI(3,1601,2),CI(3,12,1601,2),IMP(3,1601),
     1   LCHI(3,1601),XI(3,12,1601),
     1   FIVM(3,1601,2),FIKM(3,12,1601,2),FIVS(3,1601,2),
     1   FIKS(3,12,1601,2),CURI(3,1601),JV(3,1601,2),JK(3,12,1601,2)
C SPECIAL TRANSPORTERS
	DOUBLE PRECISION
     1   NP(3),KNPN(3),KNPK(3),KNH4(3),NPHK(3),
     1   LHP(3),XIHP(3),XHP(3),NAE1(3),NTSC(3),NNHE3(3),
     1   JNAK(3,3,1601,2),JHK(3,1601,2),JHP(3,1601,2),
     1   JAE1(3,1601,2),JTSC(3,1601,2),JNHE3(3,3,1601,2)
C
	DOUBLE PRECISION TCO2,AEQ,BEQ,CEQ,PAEQ,PBEQ,NH3EQ,NH4EQ,
     1    ALPH,RHO,RHON,RHOP,NU,PHI,DPHDNU,PHEQ
     1    OAEQ,OBEQ,OCEQ,OPAEQ,OPBEQ,OPHEQ,ONH3EQ,ONH4EQ
C
C	TCO2-	TOTAL CO2 IN DISQUILIBRIUM (AND EQUILIBRIUM) SOLUTIONS
C	AEQ-	EQUILIBRIUM CONCENTRATION OF H2CO3
C	BEQ-	EQUILIBRIUM CONCENTRATION OF HCO3
C	CEQ-	EQUILIBRIUM CONENTRATION OF CO2
C	PAEQ-	EQUILIBRIUM CONCENTRATION OF ACID PHOSPHATE
C	PBEQ-	EQUILIBRIUM CONCENTRATION OF BASIC PHOSPHATE
C	NH3EQ-	EQUILIBRIUM CONCENTRATION OF AMMONIA
C	NH4EQ-	EQUILIBRIUM CONCENTRATION OF AMMONIUM
C	NU-	EXPONENTIATED DISEQUILIBRIUM PH
C	O...-	EQUILIBRIUM VALUES FOR AN OPEN SYSTEM
C
C
        COMMON SOLS,T,X,
     1   Z,RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,DIST,
     1   PKC,PKP,PKN,KHY,KDHY,L0,L,
     1   VM,PM,CM,IMPM,LCHM,XM,
     1   VS,PS,CS,IMPS,LCHS,XS,ZIMPS,
     1   TL,DX,RM0,MUM,ETA,
     1   SM,AM,FVM,FKM,
     1   AME,AE0,AE,MUA,CHVL0,CHVL,MUV,
     1   LPME,LPES,SME,SES,
     1   HME,HES,CME,CES,
     1   VE,PE,CE,LCHE,XE,
     1   FEVM,FEKM,FEVS,FEKS,CURE,
     1   AIE,AI0,CLVL0,IMP0,CLVL,
     1   ZIMP,TBUF,PKB,CBUF,HCBUF,
     1   LPMI,LPIS,SMI,SIS,
     1   HMI,HIS,CMI,CIE,CIS,
     1   LMI,LIS,ATMI,ATIS,
     1   VI,PI,CI,IMP,LCHI,XI,
     1   FIVM,FIKM,FIVS,FIKS,CURI,
     1   JV,JK
	COMMON/KINET/
     1   NP,KNPN,KNPK,KNH4,NPHK,
     1   LHP,XIHP,XHP,NAE1,NTSC,NNHE3,
     1   JNAK,JHK,JHP,JAE1,JTSC,JNHE3
C
C  SOLUTE INDEX:
C	1-	NA+
C	2-	K+
C	3-	CL-
C	4-	HCO3-
C	5-	H2CO3
C	6-	CO2
C	7-	HPO4--
C	8-	H2PO4-
C	9-	UREA
C	10-	NH3
C	11-	NH4+
C	12-	H+
C
C
	ALPH = KHY(5)/KDHY(5)
	RHO  = 10.**(LCHM(X)-PKC)
	RHON = 10.**(LCHM(X)-PKN)
	RHOP = 10.**(LCHM(X)-PKP)
	TCO2 =CM(4,X,T) + CM(5,X,T) + CM(6,X,T)    
	NU = 1.0
C
C
   10   ITER=0
   15   ITER=ITER+1
	PHI = ALPH*NU*RHO*TCO2/(1. + ALPH + ALPH*NU*RHO)
     1   - CM(4,X,T) + (NU - 1.)*CM(7,X,T)/(1. + NU*RHOP)
     1    + (NU - 1.)*CM(10,X,T)/(1. + NU*RHON)
	IF(DABS(PHI).LT.1.D-16) GO TO 50
C
C PHI IS TOO LARGE.
C   WE DETERMINE THE DERIVATIVE OF PHI WITH RESPECT TO NU.
C
	DPHDNU = ALPH*(1.+ALPH)*RHO*TCO2/((1. + ALPH + ALPH*NU*RHO)**2)
     1    + (1. + RHOP)*CM(7,X,T)/((1. + NU*RHOP)**2)
     1    + (1. + RHON)*CM(10,X,T)/((1. + NU*RHON)**2)
C
C IT REMAINS TO COMPUTE C NDERIV-1(PHI) AT THE CORRECTION TO BE 
C  SUBTRACTED FROM NU.
C
	NU = NU - PHI/DPHDNU
	IF (ITER.LT.50) GO TO 15
C
C IF THE SPATIAL ITERATION FAILS TO CONVERGE
	PRINT 40
   40   FORMAT (' TOO MANY ITERATIONS IN EQLIB')
	RETURN
C
C THE SPATIAL STEP CONVERGED,AND
C THE PROGRAM PICKS UP AT THIS POINT:
C
   50   BEQ = ALPH*NU*RHO*TCO2/(1. + ALPH + ALPH*NU*RHO)
	AEQ = BEQ/(NU*RHO)
	CEQ = AEQ/ALPH
	PBEQ = NU*(1. + RHOP)*CM(7,X,T)/(1. + NU*RHOP) 
	PAEQ = PBEQ/(NU*RHOP)
	PHEQ = LCHM(X) + DLOG10(NU)
	NH3EQ = NU*(1. + RHON)*CM(10,X,T)/(1. + NU*RHON) 
	NH4EQ = NH3EQ/(NU*RHON)
C
C OUTPUT THE DATA
C
	WRITE (13,60) PHEQ,BEQ,AEQ,CEQ,PBEQ,PAEQ,NH3EQ,NH4EQ,
     1   LCHM(X),CM(4,X,T),CM(5,X,T),CM(6,X,T),
     1   CM(7,X,T),CM(8,X,T),CM(10,X,T),CM(11,X,T),
     1   BEQ-CM(4,X,T),AEQ-CM(5,X,T),CEQ-CM(6,X,T),
     1   PBEQ-CM(7,X,T),PAEQ-CM(8,X,T),NH3EQ-CM(10,X,T),NH4EQ-CM(11,X,T)
   60   FORMAT(15X,'PH',7X,'HCO3',5X,'H2CO3',7X,'CO2',
     1   6X,'HPO4',5X,'H2PO4',7X,'NH3',7X,'NH4',
     1   /,3X,'EQLIB',2X,2F10.6,E10.3,3F10.6,E10.3,F10.6,
     1   /,2X,'END-LUM',1X,2F10.6,E10.3,3F10.6,E10.3,F10.6,
     1   /,4X,'DIFF',12X,F10.6,E10.3,3F10.6,E10.3,F10.6)
C
	SCO2=BEQ-CM(4,X,T)+AEQ-CM(5,X,T)+CEQ-CM(6,X,T)
	ZS= -(BEQ-CM(4,X,T)) 
     1      -(PBEQ-CM(7,X,T)) + (NH4EQ-CM(11,X,T))
	WRITE(13,62) SCO2,ZS
   62   FORMAT(/,16X,'NET CO2 CHANGE',E10.4,17X,'NET H+ CHANGE',E10.4)
C
C
C NOW COMPUTE THE VALUES FOR AN OPEN SYSTEM
C
	OCEQ = CM(6,X,T)
	OAEQ = ALPH*OCEQ
C
  110   ITER=0
  115   ITER=ITER+1
	PHI = ALPH*NU*RHO*OCEQ  - CM(4,X,T) 
     1    + (NU - 1.)*CM(7,X,T)/(1. + NU*RHOP)
     1    + (NU - 1.)*CM(10,X,T)/(1. + NU*RHON)
	IF(DABS(PHI).LT.1.D-16) GO TO 150
C
C PHI IS TOO LARGE.
C   WE DETERMINE THE DERIVATIVE OF PHI WITH RESPECT TO NU.
C
	DPHDNU = ALPH*RHO*OCEQ
     1    + (1. + RHOP)*CM(7,X,T)/((1. + NU*RHOP)**2)
     1    + (1. + RHON)*CM(10,X,T)/((1. + NU*RHON)**2)
C
C IT REMAINS TO COMPUTE C NDERIV-1(PHI) AT THE CORRECTION TO BE 
C  SUBTRACTED FROM NU.
C
	NU = NU - PHI/DPHDNU
	IF (ITER.LT.50) GO TO 115
C
C IF THE SPATIAL ITERATION FAILS TO CONVERGE
	PRINT 140
  140   FORMAT (' TOO MANY ITERATIONS IN EQLIB')
	STOP
C
C THE SPATIAL STEP CONVERGED,AND
C THE PROGRAM PICKS UP AT THIS POINT:
C
  150   OBEQ = ALPH*NU*RHO*OCEQ
	OPBEQ = NU*(1. + RHOP)*CM(7,X,T)/(1. + NU*RHOP) 
	OPAEQ = OPBEQ/(NU*RHOP)
	ONH3EQ = NU*(1. + RHON)*CM(10,X,T)/(1. + NU*RHON) 
	ONH4EQ = ONH3EQ/(NU*RHON)
	OPHEQ = LCHM(X) + DLOG10(NU)
C
C OUTPUT THE DATA FOR THE OPEN SYSTEM
C
	WRITE (15,60) OPHEQ,OBEQ,OAEQ,OCEQ,
     1   OPBEQ,OPAEQ,ONH3EQ,ONH4EQ,
     1   LCHM(X),CM(4,X,T),CM(5,X,T),CM(6,X,T),
     1   CM(7,X,T),CM(8,X,T),CM(10,X,T),CM(11,X,T),
     1   OBEQ-CM(4,X,T),OAEQ-CM(5,X,T),
     1   OCEQ-CM(6,X,T),
     1   OPBEQ-CM(7,X,T),OPAEQ-CM(8,X,T),
     1   ONH3EQ-CM(10,X,T),ONH4EQ-CM(11,X,T)
C
	SCO2=OBEQ-CM(4,X,T)+OAEQ-CM(5,X,T)+
     1   OCEQ-CM(6,X,T)
	ZS= -(OBEQ-CM(4,X,T)) 
     1      -(OPBEQ-CM(7,X,T)) + (ONH4EQ-CM(11,X,T))
	WRITE(15,62) SCO2,ZS
	RETURN
	END
