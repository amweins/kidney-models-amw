	SUBROUTINE AHLMRESF(CHOP)
C A NEW SUBROUTINE THAT OUTPUTS SOLUTE FLUXES POINTWISE - SORTED BY SPECIES
C BASED ON FRESUL IN AHL DIRECTORY
C
	INTEGER SOLS,X,T,CHOP
C
C GENERAL PARAMETERS
	DOUBLE PRECISION
     1   Z(12),RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,DIST,
     1   PKC,PKP,PKN,KHY(5),KDHY(5),L0,L(1601,2)
C LUMINAL AND PERITUBULAR PARAMETERS
	DOUBLE PRECISION
     1   VM(1601,2),PM(1601,2),CM(12,1601,2),
     1   IMPM(1601,2),LCHM(1601),XM(12,1601),
     1   VS(1601,2),PS(1601,2),CS(12,1601,2),
     1   IMPS(1601,2),LCHS(1601),XS(12,1601),ZIMPS,
     1   TL,DX,RM0,MUM,ETA,
     1   SM(1601,2),AM(1601,2),FVM(1601,2),FKM(13,1601,2)
C INTERSPACE PARAMETERS
	DOUBLE PRECISION
     1   AME,AE0,AE(1601,2),MUA,CHVL0,CHVL(1601,2),MUV,
     1   LPME,LPES,SME(12),SES(12),
     1   HME(12),HES(12),CME(12),CES(12),
     1   VE(1601,2),PE(1601,2),CE(12,1601,2),LCHE(1601),XE(12,1601),
     1   FEVM(1601,2),FEKM(12,1601,2),FEVS(1601,2),FEKS(12,1601,2),
     1   CURE(1601)
C CELL PARAMETERS
	DOUBLE PRECISION
     1   AIE(3),AI0(3),CLVL0(3),IMP0(3),CLVL(3,1601,2),
     1   ZIMP(3),TBUF(3),PKB(3),CBUF(3,1601,2),HCBUF(3,1601,2),
     1   LPMI(3),LPIS(3),SMI(3,12),SIS(3,12),
     1   HMI(3,12),HIS(3,12),CMI(3,12),CIE(3,12),CIS(3,12),
     1   LMI(3,12,12),LIS(3,12,12),
     1   ATMI(3,12,1601),ATIS(3,12,1601),ATIE(3,12,1601),
     1   VI(3,1601,2),PI(3,1601,2),CI(3,12,1601,2),IMP(3,1601),
     1   LCHI(3,1601),XI(3,12,1601),
     1   FIVM(3,1601,2),FIKM(3,12,1601,2),FIVS(3,1601,2),
     1   FIKS(3,12,1601,2),CURI(3,1601),JV(3,1601,2),JK(3,12,1601,2)
C SPECIAL TRANSPORTERS
	DOUBLE PRECISION
     1   NP(3),KNPN(3),KNPK(3),KNH4(3),NPHK(3),
     1   LHP(3),XIHP(3),XHP(3),NAE1(3),NTSC(3),NNHE3(3),
     1   JNAK(3,3,1601,2),JHK(3,1601,2),JHP(3,1601,2),
     1   JAE1(3,1601,2),JTSC(3,1601,2),JNHE3(3,3,1601,2),
     1   NNKCC(3),MKCC(3),JNKCC(3,4,1601,2),JKCC(3,3,1601,2)
C
	DOUBLE PRECISION 
     1    PIKM(3,12,1601),PJK(3,12,1601),PIKS(3,12,1601),
     1    CIKM(3,12,1601),CJK(3,12,1601),CIKS(3,12,1601),
     1    GIKM(3,12,1601),GJK(3,12,1601),GIKS(3,12,1601)
C
C	PIKM-	ACTIVE TRANSPORT OF SOLUTE ACROSS APICAL MEMBRANE
C	PJK-	ACTIVE TRANSPORT OF SOLUTE ACROSS LATERAL MEMBRANE
C	PIKS-	ACTIVE TRANSPORT OF SOLUTE ACROSS BASAL MEMBRANE
C	CIKM-	COTRANSPORT OF SOLUTE ACROSS APICAL MEMBRANE
C	CJK-	COTRANSPORT OF SOLUTE ACROSS LATERAL MEMBRANE
C	CIKS-	COTRANSPORT OF SOLUTE ACROSS BASAL MEMBRANE
C	GIKM-	CHANNEL FLUX OF SOLUTE ACROSS APICAL MEMBRANE
C	GJK-	CHANNEL FLUX OF SOLUTE ACROSS LATERAL MEMBRANE	
C	GIKS-	CHANNEL FLUX OF SOLUTE ACROSS BASAL MEMBRANE
C
	CHARACTER*5 SOL(12)
C
	DOUBLE PRECISION FLUXS(12,1601),SCALE
C
C	FLUXS-	TOTAL EPITHELIAL SOLUTE FLUX
C	SCALE-  FACTOR TO TRANSLATE FROM MMOL/S*CM2 TO PMOL/MIN*MM
C		=PI*DIAM*(1.0E+09)*60/10
C			DIAM = 0.0030 cm  => SCALE = 5.65E+07
C			DIAM = 0.0016 cm  => SCALE = 3.02E+07
C
	DOUBLE PRECISION JNAHS(1601),JCLB(1601),JNAP(1601),JNAB(1601)
C
C
        COMMON SOLS,T,X,
     1   Z,RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,DIST,
     1   PKC,PKP,PKN,KHY,KDHY,L0,L,
     1   VM,PM,CM,IMPM,LCHM,XM,
     1   VS,PS,CS,IMPS,LCHS,XS,ZIMPS,
     1   TL,DX,RM0,MUM,ETA,
     1   SM,AM,FVM,FKM,
     1   AME,AE0,AE,MUA,CHVL0,CHVL,MUV,
     1   LPME,LPES,SME,SES,
     1   HME,HES,CME,CES,
     1   VE,PE,CE,LCHE,XE,
     1   FEVM,FEKM,FEVS,FEKS,CURE,
     1   AIE,AI0,CLVL0,IMP0,CLVL,
     1   ZIMP,TBUF,PKB,CBUF,HCBUF,
     1   LPMI,LPIS,SMI,SIS,
     1   HMI,HIS,CMI,CIE,CIS,
     1   LMI,LIS,ATMI,ATIS,ATIE,
     1   VI,PI,CI,IMP,LCHI,XI,
     1   FIVM,FIKM,FIVS,FIKS,CURI,
     1   JV,JK
	COMMON/KINET/
     1   NP,KNPN,KNPK,KNH4,NPHK,
     1   LHP,XIHP,XHP,NAE1,NTSC,NNHE3,
     1   JNAK,JHK,JHP,JAE1,JTSC,JNHE3,
     1   NNKCC,MKCC,JNKCC,JKCC
C
C
	SOL(1)='  NA '
	SOL(2)='  K  '
	SOL(3)='  CL '
	SOL(4)=' HCO3'
	SOL(5)='H2CO3'
	SOL(6)=' CO2 '
	SOL(7)=' HPO4'
	SOL(8)='H2PO4'
	SOL(9)=' UREA'
	SOL(10)=' NH3 '
	SOL(11)=' NH4 '
	SOL(12)='  H  '
C
	NCELLS=1
C
C SCALE FOR A 30 MICRON TUBULE DIAMETER
C	SCALE=5.65D+07
C SCALE FOR A 20 MICRON TUBULE DIAMETER
	SCALE=3.77D+07
C SCALE FOR A 16 MICRON TUBULE DIAMETER
C	SCALE=3.02D+07
C SCALE FOR A 15 MICRON TUBULE DIAMETER
C	SCALE=2.83D+07
C
	DO 570 KX=X-CHOP,X
	DO 520 I=1,SOLS
	FEKM(I,KX,T)=FEKM(I,KX,T)*SCALE
  520   FEKS(I,KX,T)=FEKS(I,KX,T)*SCALE
	DO 530 K=1,1
	DO 530 I=1,SOLS
	FIKM(K,I,KX,T)=FIKM(K,I,KX,T)*SCALE
	JK(K,I,KX,T)=JK(K,I,KX,T)*SCALE
  530   FIKS(K,I,KX,T)=FIKS(K,I,KX,T)*SCALE
C
	DO 550 I=1,SOLS
  550   FLUXS(I,KX)=FEKM(I,KX,T)
	DO 560 K=1,1
	DO 555 I=1,SOLS
  555   FLUXS(I,KX)=FLUXS(I,KX) + FIKM(K,I,KX,T)
  560	CONTINUE
  570	CONTINUE
C
C
	DO 170 KX=X-CHOP,X
C
C NET COTRANSPORTERS
	DO 110 K=1,1
	DO 110 I=1,SOLS
	CIKM(K,I,KX)=0.
	CIKS(K,I,KX)=0.
  110   CJK(K,I,KX)=0.
C
	DO 120 K=1,1
	DO 120 I=1,SOLS
	DO 120 J=1,SOLS
	CIKM(K,I,KX)=CIKM(K,I,KX)+
     1    LMI(K,I,J)*AI0(K)*(XM(J,KX)-XI(K,J,KX))*SCALE
	CIKS(K,I,KX)=CIKS(K,I,KX)+
     1    LIS(K,I,J)*AI0(K)*(XI(K,J,KX)-XS(J,KX))*SCALE
  120   CJK(K,I,KX)=CJK(K,I,KX)+
     1    LIS(K,I,J)*AIE(K)*(XI(K,J,KX)-XE(J,KX))*SCALE
C
C ADD IN THE INDIVIDUAL COTRANSPORTERS OF THE CELL
C
C  NKCC COTRANSPORTER
	CIKM(1,1,KX) = CIKM(1,1,KX) + JNKCC(1,1,KX,T)*SCALE
	CIKM(1,2,KX) = CIKM(1,2,KX) + JNKCC(1,2,KX,T)*SCALE
	CIKM(1,3,KX) = CIKM(1,3,KX) + JNKCC(1,3,KX,T)*SCALE
	CIKM(1,11,KX) = CIKM(1,11,KX) + JNKCC(1,4,KX,T)*SCALE
C
C  KCC COTRANSPORTER
	CJK(1,2,KX) = CJK(1,2,KX) + 
     1   JKCC(1,1,KX,T)*(AIE(1)/(AIE(1)+AI0(1)))*SCALE
	CJK(1,3,KX) = CJK(1,3,KX) + 
     1   JKCC(1,2,KX,T)*(AIE(1)/(AIE(1)+AI0(1)))*SCALE
	CJK(1,11,KX) = CJK(1,11,KX) + 
     1   JKCC(1,3,KX,T)*(AIE(1)/(AIE(1)+AI0(1)))*SCALE
C
	CIKS(1,2,KX) = CIKS(1,2,KX) + 
     1   JKCC(1,1,KX,T)*(AI0(1)/(AIE(1)+AI0(1)))*SCALE
	CIKS(1,3,KX) = CIKS(1,3,KX) + 
     1   JKCC(1,2,KX,T)*(AI0(1)/(AIE(1)+AI0(1)))*SCALE
	CIKS(1,11,KX) = CIKS(1,11,KX) + 
     1   JKCC(1,3,KX,T)*(AI0(1)/(AIE(1)+AI0(1)))*SCALE
C
C  NHE3 COTRANSPORTER
	CIKM(1,1,KX)=CIKM(1,1,KX) + JNHE3(1,1,KX,T)*SCALE
	CIKM(1,11,KX)=CIKM(1,11,KX) + JNHE3(1,3,KX,T)*SCALE
	CIKM(1,12,KX)=CIKM(1,12,KX) + JNHE3(1,2,KX,T)*SCALE
C
C PUMPS
	DO 130 K=1,1
	DO 130 I=1,SOLS
	PIKM(K,I,KX)=0.
	PIKS(K,I,KX)=0.
  130   PJK(K,I,KX)=0.
C
	DO 140 K=1,1
	DO 140 I=1,SOLS
	PIKM(K,I,KX)=PIKM(K,I,KX)+AI0(K)*ATMI(K,I,KX)*SCALE
	PIKS(K,I,KX)=PIKS(K,I,KX)+AI0(K)*ATIS(K,I,KX)*SCALE
  140   PJK(K,I,KX)=PJK(K,I,KX)+AIE(K)*ATIE(K,I,KX)*SCALE
C
C THIS PORTION BACK-CALCULATES INDIVIDUAL NET COTRANSPORTERS: JNAHS,JCLB,JNAB,JNAP
C
	JNAHS(KX)= -CIKS(1,12,KX)-CJK(1,12,KX)
	JNAP(KX)= CIKS(1,7,KX)+CJK(1,7,KX)
	JCLB(KX)= CIKS(1,3,KX)+CJK(1,3,KX) - JKCC(1,2,KX,T)*SCALE
	JNAB(KX)= CIKS(1,1,KX)+CJK(1,1,KX) - 2.*JNAP(KX) + JNAHS(KX)
C
C CONDUCTANCE FLUXES
	DO 150 K=1,1
	DO 150 I=1,SOLS
	GIKM(K,I,KX)=FIKM(K,I,KX,T)-PIKM(K,I,KX)-CIKM(K,I,KX)
	GIKS(K,I,KX)=FIKS(K,I,KX,T)-PIKS(K,I,KX)-CIKS(K,I,KX)
  150   GJK(K,I,KX)=JK(K,I,KX,T)-PJK(K,I,KX)-CJK(K,I,KX)
C
  170   CONTINUE
C
C THE SOLUTE FLUXES ARE OUTPUT ACCORDING TO SOLUTE:
C   Na, K, Cl, NH4
C
	WRITE (87,300) 
  300   FORMAT(//,1X,'DIST',2X,'Na:',3X,'FLUX',6X,'FEKM',6X,'FIKM',5X,
     1   'JNKCC',5X,'JNHE3',4X,'Na,K-ATP',3X,'JNAHS',6X,'JNAB')
	DIST=DIST-TL-DX
	DO 310 KX=X-CHOP,X
	DIST=DIST+DX
  310   WRITE (87,320) DIST,FLUXS(1,KX),FEKM(1,KX,T),
     1  FIKM(1,1,KX,T),JNKCC(1,1,KX,T)*SCALE,JNHE3(1,1,KX,T)*SCALE,
     1  PJK(1,1,KX)+PIKS(1,1,KX),JNAHS(KX),JNAB(KX)
  320   FORMAT (F7.4,2X,8F10.4)
C
	WRITE (87,330) 
  330   FORMAT(//,1X,'DIST',2X,'K:',5X,'FLUX',6X,'FEKM',6X,'FIKM',5X,
     1   'JNKCC',6X,'GIKM',4X,'Na,K-ATP',4X,'JKCC',4X,'GIKS+GJK')
	DIST=DIST-TL-DX
	DO 340 KX=X-CHOP,X
	DIST=DIST+DX
  340   WRITE (87,350) DIST,FLUXS(2,KX),FEKM(2,KX,T),
     1  FIKM(1,2,KX,T),JNKCC(1,2,KX,T)*SCALE,GIKM(1,2,KX),
     1  PJK(1,2,KX)+PIKS(1,2,KX),JKCC(1,1,KX,T)*SCALE,
     1  GIKS(1,2,KX)+GJK(1,2,KX)
  350   FORMAT (F7.4,2X,8F10.4)
C
	WRITE (87,360) 
  360   FORMAT(//,1X,'DIST',2X,'Cl:',3X,'FLUX',6X,'FEKM',6X,'FIKM',5X,
     1   'JNKCC',6X,'JKCC',6X,'JCLB',4X,'GIKS+GJK')
	DIST=DIST-TL-DX
	DO 370 KX=X-CHOP,X
	DIST=DIST+DX
  370   WRITE (87,380) DIST,FLUXS(3,KX),FEKM(3,KX,T),
     1  FIKM(1,3,KX,T),JNKCC(1,3,KX,T)*SCALE,JKCC(1,2,KX,T)*SCALE,
     1  JCLB(KX),GIKS(1,3,KX)+GJK(1,3,KX)
  380   FORMAT (F7.4,2X,8F10.4)
C
	WRITE (87,390) 
  390   FORMAT(//,1X,'DIST',2X,'NH4:',3X,'FLUX',6X,'FEKM',6X,'FIKM',6X,
     1   'JNH3',5X,'JNKCC',5X,'JNHE3',6X,'GIKM',4X,'Na,K-ATP',4X,'JKCC',
     1   4X,'GIKS+GJK',4X,'JNH3')
	DIST=DIST-TL-DX
	DO 400 KX=X-CHOP,X
	DIST=DIST+DX
  400   WRITE (87,410) DIST,FLUXS(11,KX),FEKM(11,KX,T),FIKM(1,11,KX,T),
     1  FIKM(1,10,KX,T),JNKCC(1,4,KX,T)*SCALE,JNHE3(1,3,KX,T)*SCALE,
     1  GIKM(1,11,KX), PJK(1,11,KX)+PIKS(1,11,KX),JKCC(1,3,KX,T)*SCALE,
     1  GIKS(1,11,KX)+GJK(1,11,KX),JK(1,10,KX,T)+FIKS(1,10,KX,T)
  410   FORMAT (F7.4,2X,11F10.4)
C
	DO 670 KX=X-CHOP,X
	DO 620 I=1,SOLS
	FEKM(I,KX,T)=FEKM(I,KX,T)/SCALE
  620   FEKS(I,KX,T)=FEKS(I,KX,T)/SCALE
	DO 630 K=1,1
	DO 630 I=1,SOLS
	FIKM(K,I,KX,T)=FIKM(K,I,KX,T)/SCALE
	JK(K,I,KX,T)=JK(K,I,KX,T)/SCALE
  630   FIKS(K,I,KX,T)=FIKS(K,I,KX,T)/SCALE
  670	CONTINUE
C
  700	CONTINUE
	RETURN
	END
