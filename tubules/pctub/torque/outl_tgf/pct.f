	PROGRAM PCT
C
C COMP:COMPARTMENT MODEL FOR EPITHELIAL FLUXES
C REVISED 12/12/94 (STARTING WITH PROXIMAL TUBULE MODEL IN NAH)
C TO REPRESENT THE CORTICAL COLLECTING DUCT EPITHELIUM
C WILL SUPERCEDE THE STRIETER MODEL BY ADDING CO2, H2CO3, NH3, NH4
C MODEL DERIVED FROM CCT-COMP FOR IMCD (PURE PRINCIPAL CELL)
C RECYCLED BACK TO REPRESENT PROXIMAL TUBULE EPITHELIUM USING THE
C SAME SOLUTE NOTATION OF THE DISTAL NEPHRON SEGMENTS (2/7/06)
C AND ALLOWING FOR SIMULATIONS OF CA INHIBITION
C
C   OLD PROXIMAL TUBULE DESCRIPTION:
C COMP:COMPARTMENT MODEL FOR FLUX ACROSS PROXIMAL TUBULE.
C THERE ARE 6 IONIC SPECIES: SODIUM,POTASSIUM,CHLORIDE, AND 
C BICARBONATE, AS WELL AS THE TWO PHOSPHATE IONS, AND AN
C IMPERMEANT SPECIES. IN ADDITION, TWO NONELECTROLYTES,
C GLUCOSE AND UREA, ARE NOW INCLUDED (1/12/84).  
C THE FORMATE PLUS FORMIC ACID AND AMMONIA PLUS AMMONIUM PAIRS
C ARE ALSO INCLUDED (7/9/91).  SUBSCRIPTS:
C   M-MUCOSA,  S-SEROSA,  I-INTRACELLULAR,  E-EXTRACELLULAR.
C SODIUM EXTRUSION IS A LINEAR FUNCTION OF CELL SODIUM CONC.
C ION-ION COUPLING AT THE MUCOSAL AND BASOLATERAL CELL MEMBRANES
C IS PERMITTED.  THIS INCLUDES NA-(HCO3)3 COTRANSPORT AND
C NA-(HCO3)2/CL EXCHANGE (7/9/91).
C THIS MODEL INCLUDES CYTOSOLIC PRODUCTION OF NH3 (9/93)
C   SIMULATION OF NA/H EXCHANGER WITH COMPETITIVE INHIBITOR
C FORMULATION BASED ON HEINZ: MECHANICS AND ENERGETICS OF BIOLOGICAL TRANSPORT
C DERIVATION FOLLOWS THAT OF EQUATION 6-18 WITH INCLUSION OF INHIBITOR
C 4/14/94 ALLOWS FOR CYTOSOLIC PH DEPENDENCE OF NA PERMEATION COEFFICIENT
C
C TGF ADJUSTS SNGFR ACCORDING TO END-PROXIMAL FLOW
C    AND EARLY PT PRESSURE ACCORDING TO END-PROXIMAL PRESSURE.
C
C
	INTEGER SOLS,TAU,T,TLIM,COUNT,EXP,NPAR,X,CHOP
C
C       SOLS-   NUMBER OF SOLUTES
C       TAU-    INDICATES STEADY STATE (=0) OR TIMED (=1) EXPT.
C       T-      INDEX FOR OLD (=1) OR NEW (=2) TIME STEP
C       COUNT-  NUMBER OF EXPT. BEING SIMULATED
C       EXP-    TOTAL NUMBER OF EXPTS. IN THE RUN
C	NPAR-	NUMBER OF PARAMETERS ICREMENTED IN A TRANSIENT PROBLEM.
C	X-	INDICATES SPATIAL STEP
C	CHOP-	SPATIAL CHOP OF TUBULE
C
C GENERAL PARAMETERS
	DOUBLE PRECISION
     1   Z(15),RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,L0,L(81,2),
     1   PKC,PKF,PKN,PKP,KHY(5),KDHY(5)
C LUMINAL AND PERITUBULAR PARAMETERS
	DOUBLE PRECISION
     1   VM(81,2),PM(81,2),CM(15,81,2),
     1   IMPM(81,2),LCHM(81),XM(15,81),
     1   VS(81,2),PS(81,2),CS(15,81,2),
     1   IMPS(81,2),LCHS(81),XS(15,81),
     1   TL,DX,RM0,MUM,ETA,
     1   SM(81,2),AM(81,2),FVM(81,2),FKM(16,81,2)
C INTERSPACE PARAMETERS
	DOUBLE PRECISION
     1   AME,AE0,AE(81,2),MUA,CHVL0,CHVL(81,2),MUV,
     1   LPME,LPES,SME(15),SES(15),
     1   HME(15),HES(15),CME(15),CES(15),
     1   VE(81,2),PE(81,2),CE(15,81,2),LCHE(81),XE(15,81),
     1   FEVM(81,2),FEKM(15,81,2),FEVS(81,2),FEKS(15,81,2),CURE(81)
C CELL PARAMETERS
	DOUBLE PRECISION
     1   AIE(3),AMI(3),AIS(3),CLVL0(3),IMP0(3),CLVL(3,81,2),
     1   ZIMP(3),TBUF(3),PKB(3),CBUF(3,81,2),HCBUF(3,81,2),
     1   LPMI(3),LPIS(3),SMI(3,15),SIS(3,15),
     1   HMI(3,15),HIS(3,15),CMI(3,15),CIE(3,15),CIS(3,15),
     1   LMI(3,15,15),LIS(3,15,15),ATMI(3,15,81),ATIS(3,15,81),
     1   VI(3,81,2),PI(3,81,2),CI(3,15,81,2),IMP(3,81),LCHI(3,81),
     1   XI(3,15,81),FIVM(3,81,2),FIKM(3,15,81,2),FIVS(3,81,2),
     1   FIKS(3,15,81,2),CURI(3,81),JV(3,81,2),JK(3,15,81,2)
C SPECIAL TRANSPORTERS
	DOUBLE PRECISION
     1   NP(3),KNPN(3),KNPK(3),KNH4(3),NPHK(3),
     1   LHP(3),XIHP(3),XHP(3),NAE1(3),NTSC(3),NNHE3(3),
     1   JNAK(3,3,81,2),JHK(3,81,2),JHP(3,81,2),
     1   JAE1(3,81,2),JTSC(3,81,2),JNHE3(3,3,81,2),QIAMM
C TORQUE AND COMPLIANCE VARIABLES
	DOUBLE PRECISION
     1   TQM(81,2),RM(81,2),MUR,SCALMI,SCALIS,VM0,AM0,TQM0,
     1   LHP0(3),NP0(3),NNHE30(3),LPMI0(3),LPIS0(3),
     1   HMI0(3,15),HIS0(3,15),LMI0(3,15,15),LIS0(3,15,15),
     1   QIAMM0,MVL,MVD,RMT0
C
C   SUBSCRIPTS (CELL TYPE,SOLUTE,DISTANCE,TIME)
C     INDICES FOR THE CELL TYPE: 1=PRINCIPAL; 2=ALPHA; 3=BETA
C
C       Z-      VALENCE OF I'TH SOLUTE
C       ZIMP-   VALENCE OF IMPERMEANT SPECIES
C       RT-     GAS CONST. TIMES TEMP  (MMHG.ML/MMOL)
C       RTE-    GAS CONST. TIMES TEMP  (JOULE/MMOL)
C       F-      FARADAY (COUL/MOL)
C
C       EPSI-   TOLERANCE FOR THE ERROR VECTOR
C       DT-     TIME STEP
C       RTAU-   REAL REPRESENTATION OF THE INTEGER TAU
C	TIME-	ELAPSED TIME OF THE SIMULATED EXPT.
C
C       L-      CELL HEIGHT
C       L0-     CELL MEMBRANE HEIGHT
C       MUA-    CHANNEL AREA COMPLIANCE
C       MUV-    CHANNEL VOLUME COMPLIANCE
C
C       AME-    AREA OF TIGHT JUNCTION
C       AE-     CHANNEL AREA AT BASE
C       AMI-    LUMINAL CELL AREA
C       AIE-    LATERAL CELL AREA
C       AIS-    BASAL CELL AREA
C
C       CHVL-   CHANNEL VOLUME
C       CLVL-   CELL VOLUME
C       CHVL0-  REFERENCE CELL VOLUME
C       CLVL0-  REFERENCE CELL VOLUME
C       IMP0-  CELL IMPERMEANT SPECIES CONC. AT REFERENCE VOLUME
C       IMP-    CELL IMPERMEANT SPECIES CONC. AT CLVL
C
C	CBUF-	CELL BUFFER CONCENTRATION
C	HCBUF-	PROTONATED CELL BUFFER
C	TBUF-	TOTAL CELL BUFFER CONCENTRATION FOR THE REFERENCE VOLUME
C	PK.-	.=B,C,P  FOR PK OF CELL BUFFERS, HCO3, OR HPO4
C	KHY.,KDHY.-  .=P,A,B,E HYDRATION/DEHYDRATION CONSTANT FOR CO2
C
C       LP..-   WATER PERMEABILITY (CM/SEC.MMHG)
C       S..-    REFLECTION COEFFICIENT
C       H..-    SOLUTE PERMEABILITY (CM/SEC)
C       C..-    MEAN SOLUTE CONCENTRATION
C	        ME-     TIGHT JUNCTION
C               MI-     CELL MUCOSA
C               IE-     LATERAL CELL
C               ES-     CHANNEL BASEMENT MEMBRANE
C               IS-     CELL SEROSA
C
C       LM,LS-  MATRICES OF ONSAGER COEFFICIENTS FOR MUCOSAL
C               AND BASOLATERAL CELL MEMBRANES
C
C       V.-     VOLTAGE (MVOLT)
C       P.-     PRESSURE (MMHG)
C       C.-     CONCENTRATION (MMOL/ML)
C       X.-     ELECTROCHEMICAL POTENTIAL
C	LCH.-	LOG CONCENTRATION HYDROGEN (PH)
C               . = M,S,I,E
C
C       IMP.-   IMPERMEANT SPECIES CONCENTRATION
C		. = M,S
C
C       NP-     NA+ EXTRUSION OUT THE LATERAL CELL MEMBRANE
C	KNPN-	NA AFFINITY FOR THE PUMP
C	KNPK-	K AFFINITY FOR THE PUMP
C       KNH4-   RELATIVE AFFINITY FOR NH4+ FOR THE PUMP
C	ATMI-	ACTIVE TRANSPORT ACROSS THE LUMINAL MEMBRANE
C	ATIS-	ACTIVE TRANSPORT ACROSS THE PERITUBULAR MEMBRANE
C	NPHK-	ABUNDANCE OF H-K-ATPASE
C	NAE1-	ABUNDANCE OF AE1
C	NTSC-	ABUNDANCE OF TSC
C	NNHE3-	ABUNDANCE OF NHE3
C
C	JNAK-	FLUX (MMOL/S) ACROSS THE PERITUBULAR NA,K-ATPASE
C	JHK-	FLUX ACROSS THE LUMINAL H,K-ATPASE
C	JHP-	FLUX ACROSS THE H-ATPASE
C	JAE1-	FLUX ACROSS AE1
C	JTSC-	FLUX ACROSS TSC
C	JNHE3-	FLUX ACROSS NHE3
C
C       FEVM-   JUCTION VOLUME FLOW (ML/SEC)
C       FEKM-   JUNCTION SOLUTE FLUX (MMOL/SEC)
C       FIVM-   CELL APICAL VOLUME FLOW
C       FIKM-   CELL APICAL SOLUTE FLUX
C       FEVS-   CHANNEL BASEMENT MEMBRANE VOLUME FLOW
C       FEKS-   CHANNEL BASEMENT MEMBRANE SOLUTE FLUX
C       FIVS-   BASAL CELL VOLUME FLOW
C       FIKS-   BASAL CELL SOLUTE FLUX
C       CURE-   CHANNEL CURRENT (MAMP)
C       CURI-   CELL CURRENT
C       JV-     LATERAL CELL MEMBRANE VOLUME FLOW
C       JS-     LATERAL CELL MEMBRANE SOLUTE FLUX
C
C	TL-	TUBULE LENGTH
C	DX-	SPATIAL STEP OF INTEGRATION
C	RM0-	TUBULE RADIUS AT ZERO TRANSTUBULAR PRESSURE
C	MUM-	NUMBER OF TUBULES AT THE START OF THE SEGMENT
C	ETA-	TUBULE FLUID VISCOSITY
C	SM-	LUMINAL CIRCUMFERENCE
C	AM-	LUMINAL AREA
C	FVM-	LUMINAL VOLUME FLOW RATE
C	FKM-	LUMINAL SOLUTE FLUX RATE
C
	DOUBLE PRECISION RJ,TFS,TPS,TNS,QFS,QPS,QNS,ZIMPS
	DOUBLE PRECISION GAMMA(80)
C
C       GAMMA-  VARIABLE VECTOR
C
C TGF VARIABLES: STARTING WITH THE BASELINE SNGFR, 
C   IT IS ADJUSTED ACCORDING TO MACULA DENSA CELL [CL-] 
C INITIAL PT PRESSURE IS ADJUSTED ACCORDING TO A DISTAL NEPHRON RESISTANCE
C
	DOUBLE PRECISION 
     1   FVM0,AFVM,DFVM0,DFVM1,AFVM0,FVM1,DNR,
     1   TGGAM(2),TGPHI(2),TGPPHI(2),TGEPSI,TGJAC(2,2),TGJACI(2,2),
     1   TGDGAM(2),TGDELG(2),TGDETJ
C 
C	FVM0-	SNGFR READ FROM BOUND.DAT
C	AFVM-	FIXED COMPONENT OF FVM0
C	DFVM0-	COMPONENT OF FVM0 THAT CAN BE MODULATED BY TGF
C	DFVM1-	RANGE OF FVM1 FOR WHICH FVM0 GOES FROM MIN TO MAX
C	FVM1-	TARGET OUTLET FLOW
C	TGGAM-	TG FEEDBACK VARIABLE (CHANGE IN FV)
C	TGPHI-	TG FEEDBACK RELATION
C	TGEPSI-	TG FEEDBACK ERROR
C	TGJAC-	TG FEEDBACK JACOBIAN 
C	TGDETJ-	TG FEEDBACK JACOBIAN DETERMINANT
C	TGJACI-	TG FEEDBACK JACOBIAN INVERSE
C	TGDGAM-	TG FEEDBACK VARIABLE INCREMENT
C	TGDELG-	TG FEEDBACK VARIABLE NEWTON CORRECTION
C
C         TGPHI(1) = FVM(0) - AFVM[1.0 + DFVM0* DTANH(DFVM1*(1.0 - FVM(1)/FVM1))]
C         TGPHI(2) = PM(1) - DNR*FVM(1)
C	
C	THE GAMMA FOR THIS SYSTEM ARE FVM(0) AND PM(0)
C
C
        COMMON SOLS,T,X,
     1   Z,RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,DIST,
     1   PKC,PKF,PKN,PKP,KHY,KDHY,L0,L,
     1   VM,PM,CM,IMPM,LCHM,XM,
     1   VS,PS,CS,IMPS,LCHS,XS,
     1   TL,DX,RM0,MUM,ETA,
     1   SM,AM,FVM,FKM,
     1   AME,AE0,AE,MUA,CHVL0,CHVL,MUV,
     1   LPME,LPES,SME,SES,
     1   HME,HES,CME,CES,
     1   VE,PE,CE,LCHE,XE,
     1   FEVM,FEKM,FEVS,FEKS,CURE,
     1   AIE,AMI,AIS,CLVL0,IMP0,CLVL,
     1   ZIMP,TBUF,PKB,CBUF,HCBUF,
     1   LPMI,LPIS,SMI,SIS,
     1   HMI,HIS,CMI,CIE,CIS,
     1   LMI,LIS,ATMI,ATIS,
     1   VI,PI,CI,IMP,LCHI,XI,
     1   FIVM,FIKM,FIVS,FIKS,CURI,
     1   JV,JK
	COMMON/KINET/
     1   NP,KNPN,KNPK,KNH4,NPHK,
     1   LHP,XIHP,XHP,NAE1,NTSC,NNHE3,
     1   JNAK,JHK,JHP,JAE1,JTSC,JNHE3,QIAMM
	COMMON/TORQUE/
     1   TQM,RM,MUR,SCALMI,SCALIS,VM0,AM0,TQM0,
     1   LHP0,NP0,NNHE30,LPMI0,LPIS0,
     1   HMI0,HIS0,LMI0,LIS0,QIAMM0,MVL,MVD,RMT0
C
C SW1--IF ON (1) GIVES OPEN CIRCUIT CONDITIONS (VM IS A VARIABLE
C IN THE GAMMA ARRAY); IF OFF (0) READS VM.
C
C
	OPEN (20,FILE='param.dat')
	OPEN (21,FILE='result.dat')
	OPEN (22,FILE='bound.dat')
	OPEN (23,FILE='xy.dat')
	OPEN (24,FILE='guess.dat')
	OPEN (26,FILE='ptim.dat')
	OPEN (50,FILE='lumen.dat')
	OPEN (51,FILE='blood.dat')
	OPEN (52,FILE='dhlbnd.dat')
	OPEN (12,FILE='xy.co2')
	OPEN (13,FILE='torque.dat')
	OPEN (14,FILE='tgf.dat')
C
C EXPERIMENTS WILL CYCLE FROM THIS POINT.
C
C  SOLUTE INDEX:
C	1-	NA+
C	2-	K+
C	3-	CL-
C	4-	HCO3-
C	5-	H2CO3
C	6-	CO2
C	7-	HPO4--
C	8-	H2PO4-
C	9-	UREA
C	10-	NH3
C	11-	NH4+
C	12-	H+
C	13-	HCO2-
C	14-	H2CO2
C	15-	GLUC
C
C
	SOLS=15
	NN=2*(2+SOLS)+1
C
	DGAM=1.D-10
	PTOL=1.D-10
	M1=80
	IERR=0
C
	READ (24,24) (GAMMA(J),J=1,NN)
   24   FORMAT (D30.20)
	X=1
	T=1
	CALL PCTGAM(1,GAMMA)
C
	PRINT 27
   27   FORMAT(' NUMBER OF EXPERIMENTS = ',$)
	READ *, EXP
	DO 600 COUNT=1,EXP
	PRINT 55, COUNT
   55   FORMAT(1X,'EXP=',I3)
C
C TG FEEDBACK SETTINGS:
	READ(14,8) TGEPSI,AFVM0,DFVM0,DFVM1,FVM1,DNR
    8   FORMAT(6D12.4)
C
	CALL PCTPSET(TAU,TLIM,CHOP)
	DX=TL/DBLE(FLOAT(CHOP))
C
C BOUNDARY VALUES,ACTIVE TRANSPORT,AND THE TIME INCREMENT ARE
C READ. THE TIME STEP WILL CYCLE FROM THIS POINT.
C
	T=0
	TIME=0.D0
   40   T=T+1
C
C BOUNDARY VALUES ARE READ IN:
C IT IS ASSUMED THAT THE PERITUBULAR PROFILE IS A LINEAR FUNCTION OF X.
C ASSUMING EQUILIBRIUM BETWEEN DISSOLVED CO2 AND H2CO3
C
	READ(22,50) DT,FVM(1,T),
     1  DUMVM,VS(1,T),VS(CHOP+1,T),
     1  PM(1,T),PS(1,T),PS(CHOP+1,T),
     1  IMPM(1,T),IMPS(1,T),IMPS(CHOP+1,T),
     1  (CM(I,1,T),CS(I,1,T),CS(I,CHOP+1,T),I=1,11),
     1  (CM(I,1,T),CS(I,1,T),CS(I,CHOP+1,T),I=13,15)
   50   FORMAT (2D12.4,/,3F8.4,/,3F8.4,/,3F8.4,/,(3F14.9))
	LCHM(1)=PKC + DLOG10(CM(4,1,T)/CM(5,1,T))
	CM(12,1,T)=10.**(-LCHM(1))
	ZIMPS=0.D0
C
	DO 54 J=1,CHOP+1
	RJ=DBLE(FLOAT(J-1)/FLOAT(CHOP))
	VS(J,T)=(1.D0-RJ)*VS(1,T) + RJ*VS(CHOP+1,T)
	PS(J,T)=(1.D0-RJ)*PS(1,T) + RJ*PS(CHOP+1,T)
	IMPS(J,T)=(1.D0-RJ)*IMPS(1,T) + RJ*IMPS(CHOP+1,T)
	DO 51 I=1,11
   51   CS(I,J,T)=(1.-RJ)*CS(I,1,T) + RJ*CS(I,CHOP+1,T)
	DO 52 I=13,15
   52   CS(I,J,T)=(1.-RJ)*CS(I,1,T) + RJ*CS(I,CHOP+1,T)
	LCHS(J)=PKC + DLOG10(CS(4,J,T)/CS(5,J,T))
	CS(12,J,T)=10.**(-LCHS(J))
C
	TPS=CS(7,J,T)+CS(8,J,T)
	TNS=CS(10,J,T)+CS(11,J,T)
	TNF=CS(13,J,T)+CS(14,J,T)
C
	QPS=10.**(LCHS(J)-PKP)
	QNS=10.**(LCHS(J)-PKN)
	QNF=10.**(LCHS(J)-PKF)
C
	CS(8,J,T)=TPS/(1.+QPS)
	CS(7,J,T)=TPS-CS(8,J,T)
	CS(11,J,T)=TNS/(1.+QNS)
	CS(10,J,T)=TNS-CS(11,J,T)
	CS(14,J,T)=TNF/(1.+QNF)
	CS(13,J,T)=TNF-CS(14,J,T)
C
	CS(3,J,T)=IMPS(J,T)*ZIMPS + CS(1,J,T)*Z(1) + CS(2,J,T)*Z(2)
	DO 53 I=4,SOLS
   53   CS(3,J,T)=CS(3,J,T)+CS(I,J,T)*Z(I)
   54   CONTINUE
C
	TIME=TIME+DT*FLOAT(T-1)
	RTAU=DBLE(FLOAT(TAU))/DT
	IF (TAU.EQ.1) CALL PCTPTIM(NPAR)
	CALL PCTORQUE(1,CHOP)
C
C SOLVE THE TUBULE ITERATIVELY TO DETERMINE THE OUTLET PRESSURE AND FLOW
C  BY SELECTING INLET PRESSURE AND FLOW
C  AN INITIAL GUESS FOR THE INLET IS FOR POISEUILLE FLOW WITH NO REBSORPTION
C
C	PM(1,T)=PM(1,T) + TL*50.2*ETA*MUM*FVM(1,T)/(AM0**2)
	PM(1,T)=PM(1,T) + 0.5*TL*50.2*ETA*MUM*FVM(1,T)/(AM(1,T)**2)
	FVM0 = FVM(1,T)
	AFVM = AFVM0*FVM0
	IF(COUNT.EQ.1) THEN
	TGGAM(1) = 1.0
	TGGAM(2) = PM(1,1)
	ELSE
	ENDIF
	TGDGAM(1) = 0.01
	TGDGAM(2) = 0.1
	FVM(1,1) = TGGAM(1)*FVM0
	PM(1,1) = TGGAM(2)
C
C INSERT THE TGF ITERATIONS HERE
C  CALLS TO THE *NEWT PROGRAMS SHOULD NOT REREAD PARAMETERS OR BOUNDARY DATA
C
	ITER=0
  180   ITER=ITER+1
	CALL PCNEWTON(COUNT,CHOP,NN,GAMMA)
C
	PRINT 182, ITER
  182   FORMAT(16X,'TGF ITER=',I5,$)
	TGPHI(1) = 1.D9* (FVM(1,T)-
     1   AFVM*(1.0+ DFVM0* DTANH(DFVM1*(1.0-FVM(CHOP+1,T)/FVM1))))
	TGPHI(2) = PM(CHOP+1,T) - DNR*FVM(CHOP+1,T)
	PRINT 184, (TGGAM(K),K=1,2), (TGPHI(K),K=1,2)
  184   FORMAT (8X,'TGGAM =',3X,2D12.4,8X,'TGPHI=',3X,2D12.4)
	IF((DABS(TGPHI(1)).LT.TGEPSI).AND.(DABS(TGPHI(2)).LT.TGEPSI)) 
     1     GO TO 401
C
C THE PHI ARE TOO LARGE.
C
	DO 188 II=1,2
	TGGAM(II) = TGGAM(II) + TGDGAM(II)
	FVM(1,1) = TGGAM(1)*FVM0
	PM(1,1) = TGGAM(2)
	CALL PCNEWTON(COUNT,CHOP,NN,GAMMA)
C
	TGPPHI(1) = 1.D9* (FVM(1,T)-
     1   AFVM*(1.0+ DFVM0* DTANH(DFVM1*(1.0-FVM(CHOP+1,T)/FVM1))))
	TGPPHI(2) = PM(CHOP+1,T) - DNR*FVM(CHOP+1,T)
C
	TGJAC(1,II) = (TGPPHI(1) - TGPHI(1))/TGDGAM(II)
	TGJAC(2,II) = (TGPPHI(2) - TGPHI(2))/TGDGAM(II)
	TGGAM(II) = TGGAM(II) - TGDGAM(II)
	FVM(1,1) = TGGAM(1)*FVM0
	PM(1,1) = TGGAM(2)
C
  188   CONTINUE
C
	TGDETJ = TGJAC(1,1)*TGJAC(2,2) - TGJAC(1,2)*TGJAC(2,1)
	IF (DABS(TGDETJ).LT.1.D-17) THEN 
	PRINT 189, TGDETJ,((TGJAC(I,J),I=1,2),J=1,2)
  189   FORMAT ('SINGULAR MATRIX: DET= ' D12.4,4X,'TGJAC= ', 4D12.4)
	STOP
	ELSE
	TGJACI(1,1) = TGJAC(2,2)/TGDETJ
	TGJACI(1,2) = -TGJAC(1,2)/TGDETJ
	TGJACI(2,1) = -TGJAC(2,1)/TGDETJ
	TGJACI(2,2) = TGJAC(1,1)/TGDETJ
	ENDIF
C
	TGDELG(1) = TGJACI(1,1)*TGPHI(1) + TGJACI(1,2)*TGPHI(2)
	TGDELG(2) = TGJACI(2,1)*TGPHI(1) + TGJACI(2,2)*TGPHI(2)
C
	TGGAM(1) = TGGAM(1) - TGDELG(1)
	TGGAM(2) = TGGAM(2) - TGDELG(2)
C
	FVM(1,1) = TGGAM(1)*FVM0
	PM(1,1) = TGGAM(2)
C
	GO TO 180
C
C THE BOUNDARY VALUE PROBLEM WAS SOLVED AND OUTPUT PICKS UP HERE
C
  401   CONTINUE
	IF(T.EQ.2) GO TO 420
	DO 410 X=1,CHOP+1,CHOP/2
	IF(X.EQ.1) CALL PCTRESA
	DIST=TL*FLOAT(X-1)/FLOAT(CHOP)
	CALL PCTRESB
  410   CALL PCTRESC
	X=CHOP+1
  420   CALL PCTRESD(CHOP)
	CALL PCTORQUE(3,CHOP)
C
	WRITE (52,50) DT,FVM(X,T),
     1  VM(X,T),VS(X,T),VS(CHOP+1,T),
     1  PM(X,T),PS(X,T),PS(CHOP+1,T),
     1  IMPM(X,T),IMPS(X,T),IMPS(CHOP+1,T),
     1  (CM(I,X,T),CS(I,X,T),CS(I,CHOP+1,T),I=1,11),
     1  (CM(I,X,T),CS(I,X,T),CS(I,CHOP+1,T),I=13,15)
C
	IF (TAU.EQ.1) GO TO 570
C
C  THUS IN THE STEADY STATE CASE WE ARE DONE.
	PRINT 560
  560   FORMAT (' PROBLEM SOLVED FOR THE STEADY STATE')
	GO TO 600
C
C  FOR THE TRANSIENT EXPERIMENT,THE VARIABLES AT T=1 ARE SET;
C  IF NEEDED, THE GUESS FOR T=2 IS GENERATED.
C
  570   CALL PCRESET(2,CHOP)
C
C  IF TIME IS UP,STOP.
  585   TLIM=TLIM-1
	IF (TLIM.GT.0) GO TO 40
	PRINT 590
  590   FORMAT (' TIME IS UP')
C
C
  600   CONTINUE
C
	X=1
	CALL PCTGAM(2,GAMMA)
	CLOSE (24,STATUS='KEEP')
	OPEN (24,FILE='guess.new')
	WRITE (24,624) (GAMMA(J),J=1,NN)
  624   FORMAT(D30.20)
	STOP
	END
