	SUBROUTINE DHLERR(PHI)
C
	INTEGER SOLS,T,X
C
C GENERAL PARAMETERS
	DOUBLE PRECISION
     1   Z(12),RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,DIST,
     1   PKC,PKP,PKN,KHY(5),KDHY(5),L0,L(81,2)
C LUMINAL AND PERITUBULAR PARAMETERS
	DOUBLE PRECISION
     1   VM(81,2),PM(81,2),CM(12,81,2),
     1   IMPM(81,2),LCHM(81),XM(12,81),
     1   VS(81,2),PS(81,2),CS(12,81,2),
     1   IMPS(81,2),LCHS(81),XS(12,81),
     1   TL,DX,RM0,MUM,ETA,
     1   SM(81,2),AM(81,2),FVM(81,2),FKM(13,81,2)
C INTERSPACE PARAMETERS
	DOUBLE PRECISION
     1   AME,AE0,AE(81,2),MUA,CHVL0,CHVL(81,2),MUV,
     1   LPME,LPES,SME(12),SES(12),
     1   HME(12),HES(12),CME(12),CES(12),
     1   VE(81,2),PE(81,2),CE(12,81,2),LCHE(81),XE(12,81),
     1   FEVM(81,2),FEKM(12,81,2),FEVS(81,2),FEKS(12,81,2),CURE(81)
C CELL PARAMETERS
	DOUBLE PRECISION
     1   AIE(3),AI0(3),CLVL0(3),IMP0(3),CLVL(3,81,2),
     1   ZIMP(3),TBUF(3),PKB(3),CBUF(3,81,2),HCBUF(3,81,2),
     1   LPMI(3),LPIS(3),SMI(3,12),SIS(3,12),
     1   HMI(3,12),HIS(3,12),CMI(3,12),CIE(3,12),CIS(3,12),
     1   LMI(3,12,12),LIS(3,12,12),
     1   ATMI(3,12,81),ATIS(3,12,81),ATIE(3,12,81),
     1   VI(3,81,2),PI(3,81,2),CI(3,12,81,2),IMP(3,81),LCHI(3,81),
     1   XI(3,12,81),FIVM(3,81,2),FIKM(3,12,81,2),FIVS(3,81,2),
     1   FIKS(3,12,81,2),CURI(3,81),JV(3,81,2),JK(3,12,81,2)
C SPECIAL TRANSPORTERS
	DOUBLE PRECISION
     1   NP(3),KNPN(3),KNPK(3),KNH4(3),NPHK(3),
     1   LHP(3),XIHP(3),XHP(3),NAE1(3),NTSC(3),NNHE3(3),
     1   JNAK(3,3,81,2),JHK(3,81,2),JHP(3,81,2),
     1   JAE1(3,81,2),JTSC(3,81,2),JNHE3(3,3,81,2),
     1   NNKCC(3),NKCL(3),JNKCC(3,4,81,2),JKCC(3,3,81,2)
C
	DOUBLE PRECISION PHI
	DIMENSION PHI(80)
C
        COMMON SOLS,T,X,
     1   Z,RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,DIST,
     1   PKC,PKP,PKN,KHY,KDHY,L0,L,
     1   VM,PM,CM,IMPM,LCHM,XM,
     1   VS,PS,CS,IMPS,LCHS,XS,
     1   TL,DX,RM0,MUM,ETA,
     1   SM,AM,FVM,FKM,
     1   AME,AE0,AE,MUA,CHVL0,CHVL,MUV,
     1   LPME,LPES,SME,SES,
     1   HME,HES,CME,CES,
     1   VE,PE,CE,LCHE,XE,
     1   FEVM,FEKM,FEVS,FEKS,CURE,
     1   AIE,AI0,CLVL0,IMP0,CLVL,
     1   ZIMP,TBUF,PKB,CBUF,HCBUF,
     1   LPMI,LPIS,SMI,SIS,
     1   HMI,HIS,CMI,CIE,CIS,
     1   LMI,LIS,ATMI,ATIS,ATIE,
     1   VI,PI,CI,IMP,LCHI,XI,
     1   FIVM,FIKM,FIVS,FIKS,CURI,
     1   JV,JK
	COMMON/KINET/
     1   NP,KNPN,KNPK,KNH4,NPHK,
     1   LHP,XIHP,XHP,NAE1,NTSC,NNHE3,
     1   JNAK,JHK,JHP,JAE1,JTSC,JNHE3,
     1   NNKCC,NKCL,JNKCC,JKCC
C
	NCELLS=1
C ESTABLISH THE ERROR VECTORS, THE "PHI" ARRAY.
C
C   THE OPEN CIRCUIT CONDITION
	CURE(X)=0.D0
	DO 255 I=1,SOLS
  255   CURE(X)=CURE(X)+F*Z(I)*FEKM(I,X,T)
	PHI(1)=CURE(X)
	DO 259 K=1,1
 	CURI(K,X)=0.D0
	DO 257 I=1,SOLS
  257   CURI(K,X)=CURI(K,X)+F*Z(I)*FIKM(K,I,X,T)
  259   PHI(1)=PHI(1)+CURI(K,X)
C
	IF (X.EQ.1) RETURN
C
C ESTABLISH THE ERROR VECTORS, THE "PHI" ARRAY.
C  FOR THE LUMEN VARIABLES
C
	IF (T.EQ.1) GO TO 270
C
C   MASS CONSERVATION IN THE TIME-DEPENDENT CASE
	PHI(2)=(FVM(X,T)-FVM(X-1,T))/DX
     1   +SM(X,T)*FEVM(X,T)
     1            +(FVM(X,T-1)-FVM(X-1,T-1))/DX
     1   +SM(X,T-1)*FEVM(X,T-1)
     1       +2.0*RTAU*(AM(X,T)-AM(X,T-1))
	DO 260 I=1,SOLS
	PHI(2+I)=(FKM(I,X,T)-FKM(I,X-1,T))/DX
     1   +SM(X,T)*FEKM(I,X,T)
     1            +(FKM(I,X,T-1)-FKM(I,X-1,T-1))/DX
     1   +SM(X,T-1)*FEKM(I,X,T-1)
     1   +2.0*RTAU*(AM(X,T)*CM(I,X,T)-AM(X,T-1)*CM(I,X,T-1))
  260   CONTINUE
	DO 262 K=1,1
	PHI(2)=PHI(2)+
     1   (SM(X,T)*FIVM(K,X,T)+
     1   SM(X,T-1)*FIVM(K,X,T-1))
	DO 262 I=1,SOLS
	PHI(2+I)=PHI(2+I)+
     1   (SM(X,T)*FIKM(K,I,X,T)+
     1   SM(X,T-1)*FIKM(K,I,X,T-1))
  262   CONTINUE
C
C
C   AND FOR THE IMPERMEANT
C
	PHI(3+SOLS)=(FKM(13,X,T)-FKM(13,X-1,T))/DX
     1            +(FKM(13,X,T-1)-FKM(13,X-1,T-1))/DX
     1 +2.0*RTAU*(AM(X,T)*IMPM(X,T)-AM(X,T-1)*IMPM(X,T-1))
C
	GO TO 290
C
C   MASS CONSERVATION IN THE STEADY-STATE CASE
  270   PHI(2)=(FVM(X,T)-FVM(X-1,T))/DX
     1  +SM(X,T)*FEVM(X,T)
	DO 280 I=1,SOLS+1
	PHI(2+I)=(FKM(I,X,T)-FKM(I,X-1,T))/DX
     1  +SM(X,T)*FEKM(I,X,T)
  280   CONTINUE
	DO 282 K=1,1
	PHI(2)=PHI(2)+
     1   SM(X,T)*FIVM(K,X,T)
	DO 282 I=1,SOLS
	PHI(2+I)=PHI(2+I)+
     1   SM(X,T)*FIKM(K,I,X,T)
  282   CONTINUE
C
C   AND FOR THE IMPERMEANT
C
	PHI(3+SOLS)=(FKM(13,X,T)-FKM(13,X-1,T))/DX
C
  290   CONTINUE
	DO 295 I=2,3+SOLS
  295   PHI(I)=1.D6*PHI(I)
C
C
C   THE ERROR TERM FOR PROTON GENERATION IS REPLACED BY CONSERVATION
C   OF CHARGE IN THE BUFFER REACTIONS FOR SOLUTES 4,7,10 AND 12.
C
	PHI(2+SOLS)=PHI(2+4)+PHI(2+7)-
     1    PHI(2+11)-PHI(2+SOLS)
C
C   TOTAL CO2, PHOSPHATE, AND AMMONIA CONTENT:
C
	PHI(2+4)=PHI(2+4)+PHI(2+5)+PHI(2+6)
	PHI(2+7)=PHI(2+7)+PHI(2+8)
	PHI(2+10)=PHI(2+10)+PHI(2+11)
C
C   CO2, PHOSPHATE, AND AMMMONIA PH EQUILIBRIUM: 
C
	LCHM(X)=-1.*DLOG10(CM(SOLS,X,T))
	PHI(2+5)=LCHM(X)-PKC-DLOG10(CM(4,X,T)/CM(5,X,T))
	PHI(2+8)=LCHM(X)-PKP-DLOG10(CM(7,X,T)/CM(8,X,T))
	PHI(2+11)=LCHM(X)-PKN-DLOG10(CM(10,X,T)/CM(11,X,T))
C
C   HYDRATION AND DHYDRATION OF CO2
C
	IF (T.EQ.2) THEN
	PHI(2+6)=PHI(2+6)+
     1   1.0D6*AM(X,T)*(KHY(5)*CM(6,X,T)-KDHY(5)*CM(5,X,T))+
     1   1.0D6*AM(X,T-1)*(KHY(5)*CM(6,X,T-1)-KDHY(5)*CM(5,X,T-1))
	ELSE
	PHI(2+6)=PHI(2+6)+
     1   1.0D6*AM(X,T)*(KHY(5)*CM(6,X,T)-KDHY(5)*CM(5,X,T))
	ENDIF
C
C   POISEIULLE FLOW
C
	PHI(4+SOLS)=(PM(X,T)-PM(X-1,T))/DX
     1  +50.2*ETA*MUM*FVM(X,T)/(AM(X,T)**2)
C
	RETURN
	END
