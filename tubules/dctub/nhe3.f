	SUBROUTINE NHE3(JNAH,CNAH)
C
C THE PROGRAM SOLVES FOR THE STEADY-STATE NHE3
C CALL IS OF THE FORM:
C	CALL NHE3(JMI(NA),JMI(H),JMI(NH4),
C                       ECF(NA),ECF(H),ECF(NH4),ICF(NA),ICF(H),ICF(NH4))
C
	DOUBLE PRECISION CXT,KNAH(4),PNAH(5),JNAH(3)
	DOUBLE PRECISION CNAH(3,2),CSNAH(3,2),
     1    DELTA(2),EPSILON(2),SIGMA,PSNAH(3)
C
C
C	CXT-	CONCENTRATION OF TOTAL CARRIER                 (MMOL/CM2)
C	KNAH(I)-   EQUILIBRIUM CONSTANT OF BINDING             (MMOL/CM3)
C	PNAH(I)-	PERMEATION OF I                        (   /S   )
C       JNAH(I)-	FLUX OF I FROM LUMEN TO CYTOSOL       (MMOL/S*CM2)
C
C	CNAH(I,J)- SUBSTRATE (I) CONCENTRATION IN COMPARTMENT (J)
C		I=1,2,3 CORRESPOND TO NA,H,NH4
C		J=1,2   CORRESPOND TO LUMEN AND CYTOSOL
C	CSNAH(I,J)- SCALED SUBSTRATE CONCENTRATION IN COMPARTMENT (J)
C
C	KNAH(4)-   BINDING CONSTANT FOR CYTOSOLIC PROTONS      (MMOL/CM3)
C	PNAH(4);PNAH(5)-MINIMUM AND MAXIMUM VALUES
C		FOR SODIUM (PNAH(1)) PERMEATION                (   /S   )
C
C	DELTA(J)-COMPOSITE PARAMETER
C	EPSILON(J)-COMPOSITE PARAMETER                         (   /S   )
C	SIGMA-	COMPOSITE PARAMETER                            (   /S   )
C
C
	DATA KNAH/.3000D-01,.7200D-07,.2700D-01,.1000D-05/
	DATA PNAH/.8000D+04,.2400D+04,.8000D+04,.2000D+01,.0000D+00/
	CXT=1.D0
C
C   TRANSLATE CONCENTRATIONS TO THE NAH MODEL
C
	PSNAH(1)=PNAH(1)*(PNAH(4)*CNAH(2,2) + PNAH(5)*KNAH(4))
     1       /(CNAH(2,2) + KNAH(4))
	PSNAH(2)=PNAH(2)*(PNAH(4)*CNAH(2,2) + PNAH(5)*KNAH(4))
     1       /(CNAH(2,2) + KNAH(4))
	PSNAH(3)=PNAH(3)*(PNAH(4)*CNAH(2,2) + PNAH(5)*KNAH(4))
     1       /(CNAH(2,2) + KNAH(4))
C
C  CALCULATE THE FLUXES
C
	DO 110 J=1,2
	DELTA(J)=1.D0
	EPSILON(J)=0.D0
	DO 110 I=1,3
	CSNAH(I,J)=CNAH(I,J)/KNAH(I)
	DELTA(J)=DELTA(J)+CSNAH(I,J)
	EPSILON(J)=EPSILON(J)+PSNAH(I)*CSNAH(I,J)
  110   CONTINUE
C
	SIGMA=DELTA(1)*EPSILON(2) + DELTA(2)*EPSILON(1)
C
	DO 115 I=1,3
	JNAH(I)=0.D0
	DO 115 II=1,3
	JNAH(I)=JNAH(I) + (PSNAH(I)*PSNAH(II)*CXT/SIGMA)*
     1       (CSNAH(I,1)*CSNAH(II,2) - CSNAH(I,2)*CSNAH(II,1))
  115   CONTINUE
C
	RETURN
	END
