	SUBROUTINE KCC(SS,VK,VNH4)
C
C IN THIS VERSION SUBSTRATE BINDING IS ASSUMED RAPID 
C   RELATIVE TO TRANSLOCATION
C CODE IS DERIVED FROM NKCC.F BUT THIS IS KCC4 ISOFORM
C THE ORIENTATION IS FROM OUTSIDE TO INSIDE THE CELL,
C  BUT BOUNDARY VALUES ARE READ IN (INSIDE TO OUTSIDE)
C  AND NEGATIVE FLUXES ARE OUTPUT
C
	INTEGER SOLS,SUBS,VELS
C
C       SOLS-   NUMBER OF SOLUTES
C       SUBS-   NUMBER OF SUBSTRATES
C       VELS-   NUMBER OF REACTIONS
C
	DOUBLE PRECISION
     1   V,C,S,KF,KB,KEQ,SS,VK,VNH4
C
C       S-      SUBSTRATE CONCENTRATION (MMOL/ML)
C	KF-	FORWARD RATE CONSTANT = "Kon" (mol/l)^-1 * (s^-1)
C	KB-	BACKWARD RATE CONSTANT "Koff" (s^-1)
C	KEQ-	EQUILIBRIUM CONSTANT = KF/KB = Kon/Koff
C       C-	UNKNOWN CONCENTRATION (MMOL/ML)
C       V-	NET FORWARD REACTION VELOCITY
C	VK-	NET FLUX OF K (IN NA-K-2CL)  = KF(5)*C(5) - KB(5)*C(10)
C	VNH4-	NET FLUX OF NH4 (IN NA-NH4-2CL) = KF(13)*C(12) - KB(13)*C(15)
C       SS-     SUBSTRATE CONCENTRATION (MMOL/ML) -- IDENTICAL TO S
C
	DIMENSION
     1   S(10),KF(20),KB(20),KEQ(20),V(20),C(20),SS(10)
C
C PARAMETER SETS FOR EACH OF THE ISOFORMS
C
	DOUBLE PRECISION PHI,GAMMA,MAXPHI,PREMAX
	REAL*8 DELGAM,NDERIV,RPHI,EPSI
C
C       PHI-    ERROR VECTOR- TO BE ZEROED VIA NEWTON ITERATION
C       GAMMA-  VARIABLE VECTOR
C       NDERIV- NUMERICAL DERIVATIVE OF PHI WITH RESPECT TO GAMMA
C       MAXPHI- MAXIMUM COMPONENT OF THE PHI VECTOR
C       PREMAX- MAXPHI FOR THE PREVIOUS NEWTON ITERATE
C	DELGAM-	CORRECTIONS TO THE GAMMA VECTOR COMPUTED BY LES
C       EPSI-   TOLERANCE FOR THE ERROR VECTOR
C
	DIMENSION PHI(20),GAMMA(20),NDERIV(20,20),
     1    RPHI(20),DELGAM(20)
C
	REAL*8 SW,PTOL
	INTEGER PR,PFL
C
	DIMENSION PR(20),PFL(20)
C
        COMMON/KCCKIN/ SOLS,SUBS,VELS,
     1   V,C,S,KF,KB,KEQ
C
C
	DATA KF/
     1   .1000D+09 , .1000D+09 , .1000D+05 , .1000D+09 ,
     1   .1000D+09 , .3928D+05 , .1000D+09 , .1000D+09 ,
     1   .2000D+04 , .1000D+09 , 10*0.D0/
	DATA KB/
     1   .1448D+06 , .2108D+07 , .1098D+04 , .1448D+06 ,
     1   .2108D+07 , .3577D+06 , .1448D+06 , .2108D+07 ,
     1   .2196D+03 , .1448D+06 , 10*0.D0/
	DATA KEQ/
     1   .6904D+03 , .4745D+02 , .9107D+01 , .6904D+03 ,
     1   .4745D+02 , .1098D+00 , .6904D+03 , .4745D+02 ,
     1   .9107D+01 , .6904D+03 , 10*0.D0/
C
C
	DATA GAMMA/20*0.1D0/
	SOLS=9
	SUBS=6
	VELS=10
C
C  SOLUTE INDEX:
C
C Variable Concentrations:
C	C1-	E1
C	C2-	E1-K
C	C3-	E1-KCl
C	C4-	E2
C	C5-	E2-Cl
C	C6-	E2-KCl
C
C	C7-	E1-NH4
C	C8-	E1-NH4Cl
C	C9-	E2-NH4Cl
C
C Transported Solutes:
C	S1-	K(1)
C	S2-	K(2)
C	S3-	Cl(1)
C	S4-	Cl(2)
C	S5-	NH4(1)
C	S6-	NH4(2)
C
C Reactions:
C	V1-	C1 + S1 -> C2
C	V2-	C2 + S3 -> C3
C	V3-	C3 -> C6
C
C	V4-	C5 + S2 -> C6
C	V5-	C4 + S4 -> C5
C	V6-	C4 -> C1
C
C	V7-	C1 + S5 -> C7
C	V8-	C7 + S3 -> C8
C	V9-	C8 -> C9
C	V10-	C5 + S6 -> C9
C
	NN=SOLS
	EPSI=1.D-12
	PTOL=1.D-15
	M1=20
	IERR=0
C
	DO 20 I=1,SUBS
   20   S(I) = SS(I)
C
   70   ITER=0
   80   ITER=ITER+1
	IF (ITER.GT.1) PREMAX=MAXPHI
	DO 36 I=1,NN
   36   C(I)=GAMMA(I)
	CALL KCCERR(PHI)
	MAXPHI=DABS(PHI(1))
	DO 230 J=2,NN
  230   IF (MAXPHI.LT.DABS(PHI(J))) MAXPHI=DABS(PHI(J))
C	PRINT 232, ITER, MAXPHI
  232   FORMAT ('ITER=',I3,5X,'MAXPHI=',D12.4)
	IF(MAXPHI.LT.EPSI) GO TO 558
C
C
C THE PHI ARE TOO LARGE.
C   WE WILL DETERMINE THE DERIVATIVE OF THE PHI WITH RESPECT TO
C THE VARIABLES ,GAMMA, AND STORE THESE IN THE ARRAY NDERIV( , ).
C
	CALL KCCJAC(NDERIV,M1)
	GO TO 923
C
C THE JACOBIAN HAS BEEN COMPUTED NUMERICALLY. THE CURRENT GUESSES
C SIT IN GAMMA AND THE ERRORS IN PHI.  IT REMAINS TO COMPUTE
C NDERIV-1(PHI) AT THE CORRECTION TO BE SUBTRACTED FROM GAMMA.
C
  923   DO 924 J=1,NN
  924   RPHI(J)=PHI(J)
C
	CALL LES8(NDERIV,RPHI,PR,PFL,NN,SW,PTOL,DELGAM,M1)
	IF (SW) 930,982,930
C
  930   DO 940 J=1,NN
  940   GAMMA(J)=GAMMA(J)-DELGAM(J)
C
	IF (ITER.LT.25) GO TO 80
C
C IF THE SPATIAL ITERATION FAILS TO CONVERGE WITHIN 15 PASSES
C THE PROGRAM STOPS.
	PRINT 980
  980   FORMAT (' TOO MANY ITERATIONS IN KCC')
  982   STOP
C
  558   VK = -(KF(3)*C(3) - KB(3)*C(6))
	VNH4 = -(KF(9)*C(8) - KB(9)*C(9))
	RETURN
	END
	SUBROUTINE KCCERR(PHI)
C
	INTEGER SOLS,SUBS,VELS
C
	DOUBLE PRECISION
     1   V,C,S,KF,KB,KEQ
C
	DIMENSION
     1   S(10),KF(20),KB(20),KEQ(20),V(20),C(20)
C
	DOUBLE PRECISION PHI
	DIMENSION PHI(20)
C
        COMMON/KCCKIN/ SOLS,SUBS,VELS,
     1   V,C,S,KF,KB,KEQ
C
C  SOLUTE INDEX:
C
C Variable Concentrations:
C	C1-	E1
C	C2-	E1-K
C	C3-	E1-KCl
C	C4-	E2
C	C5-	E2-Cl
C	C6-	E2-KCl
C
C	C7-	E1-NH4
C	C8-	E1-NH4Cl
C	C9-	E2-NH4Cl
C
C Transported Solutes:
C	S1-	K(1)
C	S2-	K(2)
C	S3-	Cl(1)
C	S4-	Cl(2)
C	S5-	NH4(1)
C	S6-	NH4(2)
C
C Reactions:
C	V1-	C1 + S1 -> C2
C	V2-	C2 + S3 -> C3
C	V3-	C3 -> C6
C
C	V4-	C5 + S2 -> C6
C	V5-	C4 + S4 -> C5
C	V6-	C4 -> C1
C
C	V7-	C1 + S5 -> C7
C	V8-	C7 + S3 -> C8
C	V9-	C8 -> C9
C	V10-	C5 + S6 -> C9
C
C FLUXES
C
	V(1) = KEQ(1)*C(1)*S(1) - C(2)
	V(2) = KEQ(2)*C(2)*S(3) - C(3)
	V(3) = KF(3)*C(3) - KB(3)*C(6)
C
	V(4) = KEQ(4)*C(5)*S(2) - C(6)
	V(5) = KEQ(5)*C(4)*S(4) - C(5)
	V(6) = KF(6)*C(4) - KB(6)*C(1)
C
	V(7) = KEQ(7)*C(1)*S(5) - C(7)
	V(8) = KEQ(8)*C(7)*S(3) - C(8)
	V(9) = KF(9)*C(8) - KB(9)*C(9)
	V(10) = KEQ(10)*C(5)*S(6) - C(9)
C
C
C ESTABLISH THE ERROR VECTORS, THE "PHI" ARRAY.
C  FIRST, CONSERVATION OF TOTAL CARRIER (SUM OF CI = 1.0)
C
	PHI(1)=-1.D0
	DO 110 I=1,SOLS
  110   PHI(1)=PHI(1) + C(I)
C
C   MASS CONSERVATION IN THE STEADY-STATE CASE
C
	PHI(2) = V(1)
	PHI(3) = V(2)
	PHI(4) = V(3) + V(9) - V(6)
	PHI(5) = V(4)
	PHI(6) = V(5)
C
	PHI(7) = V(7)
	PHI(8) = V(8)
	PHI(9) = V(10)
C
  170   CONTINUE
	RETURN
	END
	SUBROUTINE KCCJAC(DPHIDC,M1)
C
	INTEGER SOLS,SUBS,VELS
C
	DOUBLE PRECISION
     1   V,C,S,KF,KB,KEQ
C
	DIMENSION
     1   S(10),KF(20),KB(20),KEQ(20),V(20),C(20)
C
	DOUBLE PRECISION DVDC,DPHIDC,DPHIDV
	DIMENSION DVDC(M1,M1),DPHIDC(M1,M1),DPHIDV(M1,M1)
C
        COMMON/KCCKIN/ SOLS,SUBS,VELS,
     1   V,C,S,KF,KB,KEQ
C
	DO 103 J=1,M1
	DO 103 I=1,M1
  103   DVDC(I,J)=0.D0
C
	DO 105 J=1,M1
	DO 105 I=1,M1
  105   DPHIDC(I,J)=0.D0
C
	DO 107 J=1,M1
	DO 107 I=1,M1
  107   DPHIDV(I,J)=0.D0
C
C FLUX DERIVATIVES
C
C	V(1) = KEQ(1)*C(1)*S(1) - C(2)
C	V(2) = KEQ(2)*C(2)*S(3) - C(3)
C	V(3) = KF(3)*C(3) - KB(3)*C(6)
C
	DVDC(1,1) = KEQ(1)*S(1)
	DVDC(1,2) = - 1.0
C
	DVDC(2,2) = KEQ(2)*S(3)
	DVDC(2,3) = - 1.0
C
	DVDC(3,3) = KF(3)
	DVDC(3,6) = -KB(3)
C
C
C	V(4) = KEQ(4)*C(5)*S(2) - C(6)
C	V(5) = KEQ(5)*C(4)*S(4) - C(5)
C	V(6) = KF(6)*C(4) - KB(6)*C(1)
C
	DVDC(4,5) = KEQ(4)*S(2)
	DVDC(4,6) = - 1.0
C
	DVDC(5,4) = KEQ(5)*S(4)
	DVDC(5,5) = - 1.0
C
	DVDC(6,4) = KF(6)
	DVDC(6,1) = -KB(6)
C
C
C	V(7) = KEQ(7)*C(1)*S(5) - C(7)
C	V(8) = KEQ(8)*C(7)*S(3) - C(8)
C	V(9) = KF(9)*C(8) - KB(9)*C(9)
C	V(10) = KEQ(10)*C(5)*S(6) - C(9)
C
	DVDC(7,1) = KEQ(7)*S(5)
	DVDC(7,7) = - 1.0
C
	DVDC(8,7) = KEQ(8)*S(3)
	DVDC(8,8) = - 1.0
C
	DVDC(9,8) = KF(9)
	DVDC(9,9) = -KB(9) 
C
	DVDC(10,5) = KEQ(10)*S(6)
	DVDC(10,9) = - 1.0
C
C MODEL EQUATIONS ESTABLISH DPHIDV
C
C	PHI(1)=-1.D0
C	DO 110 I=1,SOLS
C  110   PHI(1)=PHI(1) + C(I)
C
C	PHI(2) = V(1)
C	PHI(3) = V(2)
C	PHI(4) = V(3) + V(9) - V(6)
C
C	PHI(5) = V(4)
C	PHI(6) = V(5)
C
C	PHI(7) = V(7)
C	PHI(8) = V(8)
C	PHI(9) = V(10)
C
	DPHIDV(2,1) = 1.D0
	DPHIDV(3,2) = 1.D0
	DPHIDV(4,3) = 1.D0
	DPHIDV(4,9) = 1.D0
	DPHIDV(4,6) = -1.D0
C
	DPHIDV(5,4) = 1.D0
	DPHIDV(6,5) = 1.D0
C
	DPHIDV(7,7) = 1.D0
	DPHIDV(8,8) = 1.D0
	DPHIDV(9,10) = 1.D0
C
C
C ESTABLISH THE ERROR VECTORS, THE "PHI" ARRAY.
C  FIRST, CONSERVATION OF TOTAL CARRIER (SUM OF CI = 1.0)
C
	DO 110 I=1,SOLS
  110   DPHIDC(1,I)=1.D0
C
C   MASS CONSERVATION IN THE STEADY-STATE CASE
C       DPHIDC = DPHIDV*DVDC
C
	DO 150 I=2,SOLS
	DO 150 K=1,SOLS
	DO 149 J=1,VELS
  149   DPHIDC(I,K)=DPHIDC(I,K) + DPHIDV(I,J)*DVDC(J,K)
  150	CONTINUE
C
C
	RETURN
	END
