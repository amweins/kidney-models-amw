	PROGRAM COMP
C	COMP
C COMP:COMPARTMENT MODEL FOR FLUX ACROSS PROXIMAL TUBULE.
C THERE ARE 6 IONIC SPECIES: SODIUM,POTASSIUM,CHLORIDE, AND 
C BICARBONATE, AS WELL AS THE TWO PHOSPHATE IONS, AND AN
C IMPERMEANT SPECIES. IN ADDITION, TWO NONELECTROLYTES,
C GLUCOSE AND UREA, ARE NOW INCLUDED (1/12/84).  
C THE FORMATE PLUS FORMIC ACID AND AMMONIA PLUS AMMONIUM PAIRS
C ARE ALSO INCLUDED (7/9/91).  SUBSCRIPTS:
C   M-MUCOSA,  S-SEROSA,  I-INTRACELLULAR,  E-EXTRACELLULAR.
C SODIUM EXTRUSION IS A LINEAR FUNCTION OF CELL SODIUM CONC.
C ION-ION COUPLING AT THE MUCOSAL AND BASOLATERAL CELL MEMBRANES
C IS PERMITTED.  THIS INCLUDES NA-(HCO3)3 COTRANSPORT AND
C NA-(HCO3)2/CL EXCHANGE (7/9/91).
C THIS MODEL INCLUDES CYTOSOLIC PRODUCTION OF NH3 (9/93)
C
C SIMULATION OF NA/H EXCHANGER WITH COMPETITIVE INHIBITOR
C FORMULATION BASED ON HEINZ: MECHANICS AND ENERGETICS OF BIOLOGICAL TRANSPORT
C DERIVATION FOLLOWS THAT OF EQUATION 6-18 WITH INCLUSION OF INHIBITOR
C 4/14/94 ALLOWS FOR CYTOSOLIC PH DEPENDENCE OF NA PERMEATION COEFFICIENT
C 
C TO ACHIEVE A DESIRED CELL VOLUME, THE PROGRAM VARIES PERITUBULAR K CONDUCTANCE
C
C
	INTEGER SOLS,TAU,T,TLIM,COUNT,EXP,SW1
C
C       SOLS-   NUMBER OF SOLUTES
C       TAU-    INDICATES STEADY STATE (=0) OR TIMED (=1) EXPT.
C       T-      INDEX FOR OLD (=1) OR NEW (=2) TIME STEP
C       COUNT-  NUMBER OF EXPT. BEING SIMULATED
C       EXP-    TOTAL NUMBER OF EXPTS. IN THE RUN
C
C
C
	DOUBLE PRECISION
     1   Z,ZIMP,RT,RTE,F,
     1   EPSI,ESCALE,DT,RTAU,TIME,
     1   L0,MU,DIL0,EXK,ANSP,
     1   AO,AI0,AE0,SE0,FOLDS,
     1   BULGE,VLM1,IMPER,
     1   TBUF,PKB,PKC,PKP,PKF,PKN,PCO2,
     1   LPME,LPMI,LPIE,LPES,LPIS,
     1   SME,SMI,SIE,SES,SIS,
     1   HME,HMI,HIE,HES,HIS,
     1   CME,CMI,CIE,CES,CIS,
     1   LMI,LIS,QMI,QIS,LHP,XIHP,XHP
	DOUBLE PRECISION
     1   VE,PE,CE,VI,PI,CI,CBUF,HCBUF,
     1   L,AI,AE,CHVL,CLVL,IMP,DUMVM,
     1   VM,PM,CM,VS,PS,CS,IMPM,IMPS,
     1   LCHM,LCHS,LCHE,LCHI,XE,XI,XM,XS,
     1   FEVM,FEKM,FIVM,FIKM,FEVS,FEKS,FIVS,FIKS,
     1   CURE,CURI,JV,JK,NSP,QIAMM,KNH4
C
C       Z-      VALENCE OF I'TH SOLUTE
C       ZIMP-   VALENCE OF IMPERMEANT SPECIES
C       RT-     GAS CONST. TIMES TEMP  (MMHG.ML/MMOL)
C       RTE-    GAS CONST. TIMES TEMP  (JOULE/MMOL)
C       F-      FARADAY (COUL/MOL)
C
C       EPSI-   TOLERANCE FOR THE ERROR VECTOR
C       ESCALE- PHYSICAL SCALING FACTOR (CELLS/SQ.CM. EPITH.)
C       DT-     TIME STEP
C       RTAU-   REAL REPRESENTATION OF THE INTEGER TAU
C	TIME-	ELAPSED TIME OF THE SIMULATED EXPT.
C
C       L-      CELL HEIGHT
C       L0-     CELL HEIGHT WITH EQUAL MUCOSAL AND CELL PRESSURES
C       MU-     CHANNEL AREA COMPLIANCE
C       DIL0-   CHANNEL AREA WITH EQUAL CHANNEL AND CELL PRESSURES
C
C       AO-     ACTUAL AREA OF CELL+CHANNEL UNIT
C       AI0-    CROSS-SECTION AREA OF CELL AT MUCOSAL SURFACE
C       AI-     CROSS-SECTION AREA OF CELL
C       AE0-    AREA OF TIGHT JUNCTION
C       AE-     CHANNEL AREA
C       SE0-    CHANNEL CIRCUMFERENCE
C       FOLDS-  CELL MUCOSAL AREA OVER CROSS SECTION
C
C       BULGE-  FACTOR INCREASING CHANNEL VOLUME
C       CHVL-   CHANNEL VOLUME
C       CLVL-   CELL VOLUME
C       VLM1-   REFERENCE CELL VOLUME
C       IMPER-  CELL IMPERMEANT SPECIES CONC. AT REFERENCE VOLUME
C       IMP-    CELL IMPERMEANT SPECIES CONC. AT CLVL
C
C	CBUF-	CELL BUFFER CONCENTRATION
C	HCBUF-	PROTONATED CELL BUFFER
C	TBUF-	TOTAL CELL BUFFER CONCENTRATION FOR THE REFERENCE VOLUME
C	PK.-	.=B,C,P  FOR PK OF CELL BUFFERS, HCO3, OR HPO4
C
C       LP..-   WATER PERMEABILITY (CM/SEC.MMHG)
C       S..-    REFLECTION COEFFICIENT
C       H..-    SOLUTE PERMEABILITY (CM/SEC)
C       C..-    MEAN SOLUTE CONCENTRATION
C	        ME-     TIGHT JUNCTION
C               MI-     CELL MUCOSA
C               IE-     LATERAL CELL
C               ES-     CHANNEL BASEMENT MEMBRANE
C               IS-     CELL SEROSA
C
C       LM,LS-  MATRICES OF ONSAGER COEFFICIENTS FOR MUCOSAL
C               AND BASOLATERAL CELL MEMBRANES
C       QMI,QIS-MATRICES OF SOLUTE COUPLING COEFFICIENTS
C
C       V.-     VOLTAGE (MVOLT)
C       P.-     PRESSURE (MMHG)
C       C.-     CONCENTRATION (MMOL/ML)
C       X.-     ELECTROCHEMICAL POTENTIAL
C	LCH.-	LOG CONCENTRATION HYDROGEN (PH)
C               . = M,S,I,E
C
C       IMP.-   IMPERMEANT SPECIES CONCENTRATION
C		. = M,S
C	PCO2-	PARTIAL PRESSURE OF CARBON DIOXIDE (MMHG)
C       DUMVM-  DUMMY VARIABLE HOLDING VM (OPEN CIRCUIT)
C
C       NSP-    NA+ EXTRUSION OUT THE LATERAL CELL MEMBRANE
C       ANSP-   INCREMENT OF NSP WITH CELL NA+ CONC.
C       EXK-    BASOLATERAL NA-K EXCHANGE RATIO
C       QIAMM-  CYTOSOLIC PRODUCTION OF NH3
C	KNH4-	RATIO OF KNH4 TO KK FOR TRANSPORT ON NA,K-ATPASE
C
C       FEVM-   JUCTION VOLUME FLOW (ML/SEC)
C       FEKM-   JUNCTION SOLUTE FLUX (MMOL/SEC)
C       FIVM-   CELL APICAL VOLUME FLOW
C       FIKM-   CELL APICAL SOLUTE FLUX
C       FEVS-   CHANNEL BASEMENT MEMBRANE VOLUME FLOW
C       FEKS-   CHANNEL BASEMENT MEMBRANE SOLUTE FLUX
C       FIVS-   BASAL CELL VOLUME FLOW
C       FIKS-   BASAL CELL SOLUTE FLUX
C       CURE-   CHANNEL CURRENT (MAMP)
C       CURI-   CELL CURRENT
C       JV-     LATERAL CELL MEMBRANE VOLUME FLOW
C       JS-     LATERAL CELL MEMBRANE SOLUTE FLUX
C
	DOUBLE PRECISION CXT,KNAH(4),PNAH(5),JNAH(3)
C
C	CXT-	CONCENTRATION OF TOTAL CARRIER                 (MMOL/CM2)
C	KNAH(I)-   EQUILIBRIUM CONSTANT OF BINDING             (MMOL/CM3)
C	PNAH(I)-	PERMEATION OF I                        (   /S   )
C       JNAH(I)-	FLUX OF I FROM LUMEN TO CYTOSOL       (MMOL/S*CM2)
C
C	KNAH(4)-   BINDING CONSTANT FOR CYTOSOLIC PROTONS      (MMOL/CM3)
C	PNAH(4);PNAH(5)-MINIMUM AND MAXIMUM VALUES
C		FOR SODIUM (PNAH(1)) PERMEATION                (   /S   )
C
C
	DIMENSION Z(13),
     1   SME(13),SMI(13),SIE(13),SES(13),SIS(13),
     1   HME(13),HMI(13),HIE(13),HES(13),HIS(13),
     1   CME(13),CMI(13),CIE(13),CES(13),CIS(13),
     1   LMI(13,13),LIS(13,13),QMI(13,13),QIS(13,13)
	DIMENSION CM(13),CS(13),
     1   L(2),AI(2),AE(2),CHVL(2),CLVL(2),CBUF(2),HCBUF(2),
     1   VE(2),PE(2),CE(13,2),VI(2),PI(2),CI(13,2),
     1   LCHE(2),LCHI(2),XE(13),XI(13),XM(13),XS(13),
     1   FEVM(2),FEKM(13,2),FIVM(2),FIKM(13,2),
     1   FEVS(2),FEKS(13,2),FIVS(2),FIKS(13,2),
     1   CURE(2),CURI(2),JV(2),JK(13,2)
C
	DOUBLE PRECISION GAMMA(40),
     1   CLVL0,DHIEK,PHIVL,DVDHIE,EPSVL
C
C	CLVL0-	DESIRED CELL VOLUME
C	PHIVL-	ERROR BETWEEN CALCULATED AND DESIRED VOLUME
C	DVDHIE-	NUMERICAL DERIVATIVE OF CELL VOLUME WRT HIE(K)
C	DHIEK-	INCREMENT TO COMPUTE NUMERICAL DERIVATIVE 
C	EPSVL-	CONVERGENCE CRITERION
C
        COMMON/PAR/ SOLS,T,
     1   Z,ZIMP,RT,RTE,F,
     1   EPSI,ESCALE,DT,RTAU,TIME,
     1   L0,MU,DIL0,EXK,ANSP,
     1   AO,AI0,AE0,SE0,FOLDS,
     1   BULGE,VLM1,IMPER,
     1   TBUF,PKB,PKC,PKP,PKF,PKN,PCO2,
     1   LPME,LPMI,LPIE,LPES,LPIS,
     1   SME,SMI,SIE,SES,SIS,
     1   HME,HMI,HIE,HES,HIS,
     1   CME,CMI,CIE,CES,CIS,
     1   LMI,LIS,QMI,QIS,LHP,XIHP,XHP,
     1   CXT,KNAH,PNAH
	COMMON/VAR/
     1   VE,PE,CE,VI,PI,CI,CBUF,HCBUF,
     1   L,AI,AE,CHVL,CLVL,IMP,DUMVM,
     1   VM,PM,CM,VS,PS,CS,IMPM,IMPS,
     1   LCHM,LCHS,LCHE,LCHI,XE,XI,XM,XS,
     1   FEVM,FEKM,FIVM,FIKM,FEVS,FEKS,FIVS,FIKS,
     1   CURE,CURI,JV,JK,NSP,QIAMM,KNH4,JNAH
C
C SW1--IF ON (1) GIVES OPEN CIRCUIT CONDITIONS (VM IS A VARIABLE
C IN THE GAMMA ARRAY); IF OFF (0) READS VM.
C
C
	OPEN (24,FILE='guess.dat')
	OPEN (22,FILE='bound.dat')
	OPEN (21,FILE='result.dat')
	OPEN (23,FILE='xy.dat')
	OPEN (20,FILE='param.dat')
	OPEN (25,FILE='cotr.dat')
	OPEN (26,FILE='clvl.dat')
	OPEN (27,FILE='hiek.dat')
C
C EXPERIMENTS WILL CYCLE FROM THIS POINT.
C
C  SOLUTE INDEX:
C	1-	NA+
C	2-	K+
C	3-	CL-
C	4-	HCO3-
C	5-	HPO4--
C	6-	H2PO4-
C	7-	GLUC
C	8-	UREA
C	9-	CHO2-
C	10-	CH2O2
C	11-	NH3
C	12-	NH4+
C	13-	H+
C
C
	SOLS=12
	PRINT 20
   20   FORMAT (' TYPE 1/0 FOR OPEN/CLOSED CIRCUIT:  ',$)
	READ *,SW1
	NN=4+2*SOLS+SW1+2
	EPSVL=1.D-10
C
	READ (24,24) (GAMMA(J),J=1,NN)
   24   FORMAT (D30.20)
C
	PRINT 27
   27   FORMAT(' NUMBER OF EXPERIMENTS = ',$)
	READ *, EXP
	DO 600 COUNT=1,EXP
C
	READ (26,28) CLVL0
   28   FORMAT (D16.8)
C
	CALL PSET(TAU,TLIM)
	T=1
	CALL GAMSET(1,GAMMA)
C
C
C BOUNDARY VALUES,ACTIVE TRANSPORT,AND THE TIME INCREMENT ARE
C READ. THE TIME STEP WILL CYCLE FROM THIS POINT.
C
	T=0
	TIME=0.D0
   40   T=T+1
	READ(22,50) DT,ANSP,QIAMM,DUMVM,PM,IMPM,VS,PS,IMPS,
     1  (CM(I),CS(I),I=1,SOLS)
   50   FORMAT (3D12.4,/,6F8.4,/,(2F14.9))
	IF (SW1.EQ.0) VM=DUMVM
	TIME=TIME+DT*FLOAT(T-1)
	RTAU=2.D0*DBLE(FLOAT(TAU))/DT
C
C   START THE FITTING ITERATION.
C
	PRINT 72, COUNT,TIME
   72   FORMAT(5X,'EXP=',I3,5X,'TIME=',F8.3)
C
   70   ITER=0
   80   ITER=ITER+1
C
	CALL NEWTON(NN,GAMMA)
C	PHIVL=CLVL(T)-CLVL0
 	PHIVL=CI(4,T)-CLVL0
C
	PRINT 82, ITER
   82   FORMAT('FITITER=',I5,$)
	PRINT 232,PHIVL
  232   FORMAT (D12.4)
	IF (DABS(PHIVL).LT.EPSVL) GO TO 558
C
C UPDATE THE PERMEABILITY
C
	DHIEK=1.D-2*HIE(2)
C	DVDHIE=CLVL(T)
 	DVDHIE=CI(4,T)
	HIE(2)=HIE(2)+DHIEK
	HIS(2)=HIS(2)+DHIEK
	CALL NEWTON(NN,GAMMA)
C	DVDHIE=(CLVL(T)-DVDHIE)/DHIEK
	DVDHIE=(CI(4,T)-DVDHIE)/DHIEK
	HIE(2)=HIE(2)-DHIEK
	HIS(2)=HIS(2)-DHIEK
	HIE(2)=HIE(2) - PHIVL/DVDHIE
	HIS(2)=HIS(2) - PHIVL/DVDHIE
C
	IF (ITER.LT.25) GO TO 80
C
C IF THE SPATIAL ITERATION FAILS TO CONVERGE WITHIN 25 PASSES
C THE PROGRAM STOPS.
	PRINT 980
  980   FORMAT (' TOO MANY ITERATIONS IN THE FITTING ROUTINE')
	STOP
C
C THE SPATIAL ITERATION CONVERGED,RESULTS WERE OUTPUT, AND
C THE PROGRAM PICKS UP AT THIS POINT:
C
  558   IF(T.EQ.1) CALL ARESUL
	CALL BRESUL(ITER,MAXPHI)
	IF(T.EQ.1) CALL CRESUL
	CALL ERESUL
	IF (TAU.EQ.1) GO TO 570
C
C  THUS IN THE STEADY STATE CASE WE ARE DONE.
	PRINT 560
  560   FORMAT (' PROBLEM SOLVED FOR THE STEADY STATE')
	GO TO 600
C
C  FOR THE TRANSIENT EXPERIMENT,THE VARIABLES AT T=1 ARE SET;
C  IF NEEDED, THE GUESS FOR T=2 IS GENERATED.
C
  570   CALL RESET
C
C  IF TIME IS UP,STOP.
  585   TLIM=TLIM-1
	IF (TLIM.GT.0) GO TO 40
	PRINT 590
  590   FORMAT (' TIME IS UP')
C
C
  600   CONTINUE
C
	CLOSE (24,STATUS='KEEP')
	OPEN (24,FILE='guess.new')
	WRITE (24,624) (GAMMA(J),J=1,NN)
  624   FORMAT(D30.20)
	STOP
	END
