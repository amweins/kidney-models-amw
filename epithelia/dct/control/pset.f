	SUBROUTINE PSET(TAU,TLIM,SW)
C PROGRAM TO READ AND INITIALIZE THE PARAMETERS FOR COMP
C
	INTEGER SOLS,TAU,T,TLIM,SW
C
C GENERAL PARAMETERS
	DOUBLE PRECISION
     1   Z(12),RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,
     1   PKC,PKP,PKN,KHY(4),KDHY(4),L0,L(2)
C LUMINAL AND PERITUBULAR PARAMETERS
	DOUBLE PRECISION
     1   DUMVM,VM,PM,CM(12),IMPM,LCHM,XM(12),
     1   VS,PS,CS(12),IMPS,LCHS,XS(12)
C INTERSPACE PARAMETERS
	DOUBLE PRECISION
     1   AME,AE0,AE(2),MUA,CHVL0,CHVL(2),MUV,
     1   LPME,LPES,SME(12),SES(12),
     1   HME(12),HES(12),CME(12),CES(12),
     1   VE(2),PE(2),CE(12,2),LCHE,XE(12),
     1   FEVM(2),FEKM(12,2),FEVS(2),FEKS(12,2),CURE
C CELL PARAMETERS
	DOUBLE PRECISION
     1   AIE(3),AI0(3),CLVL0(3),IMP0(3),CLVL(3,2),
     1   ZIMP(3),TBUF(3),PKB(3),CBUF(3,2),HCBUF(3,2),
     1   LPMI(3),LPIS(3),SMI(3,12),SIS(3,12),
     1   HMI(3,12),HIS(3,12),CMI(3,12),CIE(3,12),CIS(3,12),
     1   LMI(3,12,12),LIS(3,12,12),ATMI(3,12),ATIS(3,12),
     1   VI(3,2),PI(3,2),CI(3,12,2),IMP(3),LCHI(3),XI(3,12),
     1   FIVM(3,2),FIKM(3,12,2),FIVS(3,2),FIKS(3,12,2),CURI(3),
     1   JV(3,2),JK(3,12,2)
C SPECIAL TRANSPORTERS
	DOUBLE PRECISION
     1   NP(3),KNPN(3),KNPK(3),KNH4(3),NPHK(3),
     1   LHP(3),XIHP(3),XHP(3),NAE1(3),NTSC(3),NNHE3(3),
     1   JNAK(3,3),JHK(3),JHP(3),JAE1(3),JTSC(3),JNHE3(3,3)
C
C CONTROL VARIABLES
C
	INTEGER  
     1   NMI(3),ORDMI(3,6),IMI(3,6,6),JMI(3,6,6),MMI(3,6,6),
     1   NIS(3),ORDIS(3,6),IIS(3,6,6),JIS(3,6,6),MIS(3,6,6)
	DOUBLE PRECISION RMI(3,6),RIS(3,6),
     1   HCON(10),HCON0(10),DHCON(10,4),CON(4),CON0(10,4)
C
C
        COMMON SOLS,T,
     1   Z,RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,
     1   PKC,PKP,PKN,KHY,KDHY,L0,L,
     1   DUMVM,VM,PM,CM,IMPM,LCHM,XM,
     1   VS,PS,CS,IMPS,LCHS,XS,
     1   AME,AE0,AE,MUA,CHVL0,CHVL,MUV,
     1   LPME,LPES,SME,SES,
     1   HME,HES,CME,CES,
     1   VE,PE,CE,LCHE,XE,
     1   FEVM,FEKM,FEVS,FEKS,CURE,
     1   AIE,AI0,CLVL0,IMP0,CLVL,
     1   ZIMP,TBUF,PKB,CBUF,HCBUF,
     1   LPMI,LPIS,SMI,SIS,
     1   HMI,HIS,CMI,CIE,CIS,
     1   LMI,LIS,ATMI,ATIS,
     1   VI,PI,CI,IMP,LCHI,XI,
     1   FIVM,FIKM,FIVS,FIKS,CURI,
     1   JV,JK
	COMMON/KINET/
     1   NP,KNPN,KNPK,KNH4,NPHK,
     1   LHP,XIHP,XHP,NAE1,NTSC,NNHE3,
     1   JNAK,JHK,JHP,JAE1,JTSC,JNHE3
	COMMON/DCTCON/
     1   NMI,ORDMI,IMI,JMI,MMI,
     1   NIS,ORDIS,IIS,JIS,MIS,RMI,RIS,
     1   HCON,HCON0,DHCON,CON,CON0
C
	IF (SW.EQ.2) GO TO 60
C
C  SOLUTE INDEX:
C	1-	NA+
C	2-	K+
C	3-	CL-
C	4-	HCO3-
C	5-	H2CO3
C	6-	CO2
C	7-	HPO4--
C	8-	H2PO4-
C	9-	UREA
C	10-	NH3
C	11-	NH4+
C	12-	H+
C
	Z(1)=1.D0
	Z(2)=1.D0
	Z(3)=-1.D0
	Z(4)=-1.D0
	Z(5)=0.D0
	Z(6)=0.D0
	Z(7)=-2.D0
	Z(8)=-1.D0
	Z(9)=0.D0
	Z(10)=0.D0
	Z(11)=1.D0
	Z(12)=1.D0
C
	RT=0.193D+05
	RTE=2.57
	F=0.965000D+05
	PKC=3.57
	PKP=6.8
	PKN=9.15
C
C 7/29/91- IN THE OLDER VERSIONS THE DIAGONAL LMI AND LIS WERE FIRST READ
C AND THEN THE COUPLING COEFFICIENTS.  NOW EACH COTRANSPORTER IS READ IN
C COEFFICIENT BY COEFFICIENT.  THIS ALLOWS MULTIPLE TRANSPORTERS TO SHARE
C A SINGLE SPECIES WITHOUT HAVING TO BACK-CALCULATE THE COUPLING COEFFICIENTS.
C
	DO 18 K=1,1
	DO 18 I=1,SOLS
	ATMI(K,I)=0.
   18   ATIS(K,I)=0.
C
C GEOMETRIC AND MEMBRANE PROPERTIES ARE READ. 
C
	READ (20,30) TAU,TLIM,EPSI,
     1  AME,AE0,MUA,KHY(4),KDHY(4),
     1  L0,CHVL0,MUV,LPME,LPES,
     1  (SME(I),SES(I),HME(I),HES(I),I=1,SOLS)
   30   FORMAT (2I5,D12.4,/,5D12.4,/,5D12.4,/,(2F6.3, 2D12.4))
C
	DO 55 K=1,1
	READ (20,25)
     1  AIE(K),AI0(K),CLVL0(K),IMP0(K),ZIMP(K),
     1  KHY(K),KDHY(K),TBUF(K),PKB(K),
     1  NP(K),KNH4(K),NPHK(K),
     1  LHP(K),XIHP(K),XHP(K),
     1  NAE1(K),NTSC(K),NNHE3(K)
	READ (20,26)
     1  LPMI(K),LPIS(K),
     1  (SMI(K,I),SIS(K,I),HMI(K,I),HIS(K,I),I=1,SOLS)
   25   FORMAT (5D12.4,/,4D12.4,/,(3D12.4))
   26   FORMAT (2D12.4,/,(2F6.3, 2D12.4))
C
	READ(20,31) NMI(K),NIS(K)
	IF(NMI(K).EQ.0) GO TO 36
	DO 44 LL=1,NMI(K)
	READ(20,42) ORDMI(K,LL),RMI(K,LL)
	DO 44 KK=1,ORDMI(K,LL)
   44   READ(20,31) IMI(K,KK,LL),JMI(K,KK,LL),MMI(K,KK,LL)
C
   36   IF(NIS(K).EQ.0) GO TO 48
	DO 46 LL=1,NIS(K)
	READ(20,42) ORDIS(K,LL),RIS(K,LL)
	DO 46 KK=1,ORDIS(K,LL)
   46   READ(20,31) IIS(K,KK,LL),JIS(K,KK,LL),MIS(K,KK,LL)
   31   FORMAT (3I5)
   42   FORMAT (I5,D12.4)
C
C CONTROL VARIABLES ARE READ
C
   48   DO 49 I=1,7
   49   READ(20,47) (CON0(I,J),J=1,4)
   47   FORMAT (4D12.4)
	DO 52 I=1,7
   52   READ(20,51) (DHCON(I,J),J=1,4)
   51   FORMAT (4F7.3)
C
   55   CONTINUE
C
C
   60   DO 80 K=1,1
C
	DO 62 I=1,SOLS+1
	DO 62 J=1,SOLS+1
	LMI(K,I,J)=0.D0
   62   LIS(K,I,J)=0.D0
C
	IF(NMI(K).EQ.0) GO TO 66
	DO 64 LL=1,NMI(K)
	DO 64 KK=1,ORDMI(K,LL)
	LMI(K,IMI(K,KK,LL),JMI(K,KK,LL))=
     1    LMI(K,IMI(K,KK,LL),JMI(K,KK,LL)) 
     1    + FLOAT(MMI(K,KK,LL))*RMI(K,LL)
   64   LMI(K,JMI(K,KK,LL),IMI(K,KK,LL))
     1    =LMI(K,IMI(K,KK,LL),JMI(K,KK,LL))
C
   66   IF(NIS(K).EQ.0) GO TO 80
	DO 68 LL=1,NIS(K)
	DO 68 KK=1,ORDIS(K,LL)
	LIS(K,IIS(K,KK,LL),JIS(K,KK,LL))=
     1    LIS(K,IIS(K,KK,LL),JIS(K,KK,LL)) 
     1    + FLOAT(MIS(K,KK,LL))*RIS(K,LL)
   68   LIS(K,JIS(K,KK,LL),IIS(K,KK,LL))
     1    =LIS(K,IIS(K,KK,LL),JIS(K,KK,LL))
C
   80   CONTINUE
	RETURN
	END
