	PROGRAM COMP
C COMP:COMPARTMENT MODEL FOR EPITHELIAL FLUXES
C REVISED 12/12/94 (STARTING WITH PROXIMAL TUBULE MODEL IN NAH)
C TO REPRESENT THE CORTICAL COLLECTING DUCT EPITHELIUM
C WILL SUPERCEDE THE STRIETER MODEL BY ADDING CO2, H2CO3, NH3, NH4
C MODEL DERIVED FROM CCT-COMP FOR IMCD (PURE PRINCIPAL CELL)
C RECYCLED BACK TO REPRESENT PROXIMAL TUBULE EPITHELIUM USING THE
C SAME SOLUTE NOTATION OF THE DISTAL NEPHRON SEGMENTS (2/7/06)
C AND ALLOWING FOR SIMULATIONS OF CA INHIBITION
C
C   OLD PROXIMAL TUBULE DESCRIPTION:
C COMP:COMPARTMENT MODEL FOR FLUX ACROSS PROXIMAL TUBULE.
C THERE ARE 6 IONIC SPECIES: SODIUM,POTASSIUM,CHLORIDE, AND 
C BICARBONATE, AS WELL AS THE TWO PHOSPHATE IONS, AND AN
C IMPERMEANT SPECIES. IN ADDITION, TWO NONELECTROLYTES,
C GLUCOSE AND UREA, ARE NOW INCLUDED (1/12/84).  
C THE FORMATE PLUS FORMIC ACID AND AMMONIA PLUS AMMONIUM PAIRS
C ARE ALSO INCLUDED (7/9/91).  SUBSCRIPTS:
C   M-MUCOSA,  S-SEROSA,  I-INTRACELLULAR,  E-EXTRACELLULAR.
C SODIUM EXTRUSION IS A LINEAR FUNCTION OF CELL SODIUM CONC.
C ION-ION COUPLING AT THE MUCOSAL AND BASOLATERAL CELL MEMBRANES
C IS PERMITTED.  THIS INCLUDES NA-(HCO3)3 COTRANSPORT AND
C NA-(HCO3)2/CL EXCHANGE (7/9/91).
C THIS MODEL INCLUDES CYTOSOLIC PRODUCTION OF NH3 (9/93)
C   SIMULATION OF NA/H EXCHANGER WITH COMPETITIVE INHIBITOR
C FORMULATION BASED ON HEINZ: MECHANICS AND ENERGETICS OF BIOLOGICAL TRANSPORT
C DERIVATION FOLLOWS THAT OF EQUATION 6-18 WITH INCLUSION OF INHIBITOR
C 4/14/94 ALLOWS FOR CYTOSOLIC PH DEPENDENCE OF NA PERMEATION COEFFICIENT
C
C
	INTEGER SOLS,TAU,T,TLIM,COUNT,EXP,SW1,NPAR
C
C       SOLS-   NUMBER OF SOLUTES
C       TAU-    INDICATES STEADY STATE (=0) OR TIMED (=1) EXPT.
C       T-      INDEX FOR OLD (=1) OR NEW (=2) TIME STEP
C       COUNT-  NUMBER OF EXPT. BEING SIMULATED
C       EXP-    TOTAL NUMBER OF EXPTS. IN THE RUN
C	NPAR-	NUMBER OF PARAMETERS ICREMENTED IN A TRANSIENT PROBLEM.
C
C GENERAL PARAMETERS
	DOUBLE PRECISION
     1   Z(15),RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,L0,L(2),
     1   PKC,PKF,PKN,PKP,KHY(4),KDHY(4)
C LUMINAL AND PERITUBULAR PARAMETERS
	DOUBLE PRECISION
     1   DUMVM,VM,PM,CM(15),IMPM,LCHM,XM(15),
     1   VS,PS,CS(15),IMPS,LCHS,XS(15)
C INTERSPACE PARAMETERS
	DOUBLE PRECISION
     1   AME,AE0,AE(2),MUA,CHVL0,CHVL(2),MUV,
     1   LPME,LPES,SME(15),SES(15),
     1   HME(15),HES(15),CME(15),CES(15),
     1   VE(2),PE(2),CE(15,2),LCHE,XE(15),
     1   FEVM(2),FEKM(15,2),FEVS(2),FEKS(15,2),CURE
C CELL PARAMETERS
	DOUBLE PRECISION
     1   AIE(3),AMI(3),AIS(3),CLVL0(3),IMP0(3),CLVL(3,2),
     1   ZIMP(3),TBUF(3),PKB(3),CBUF(3,2),HCBUF(3,2),
     1   LPMI(3),LPIS(3),SMI(3,15),SIS(3,15),
     1   HMI(3,15),HIS(3,15),CMI(3,15),CIE(3,15),CIS(3,15),
     1   LMI(3,15,15),LIS(3,15,15),ATMI(3,15),ATIS(3,15),
     1   VI(3,2),PI(3,2),CI(3,15,2),IMP(3),LCHI(3),XI(3,15),
     1   FIVM(3,2),FIKM(3,15,2),FIVS(3,2),FIKS(3,15,2),CURI(3),
     1   JV(3,2),JK(3,15,2)
C SPECIAL TRANSPORTERS
	DOUBLE PRECISION
     1   NP(3),KNPN(3),KNPK(3),KNH4(3),NPHK(3),
     1   LHP(3),XIHP(3),XHP(3),NAE1(3),NTSC(3),NNHE3(3),
     1   JNAK(3,3),JHK(3),JHP(3),JAE1(3),JTSC(3),JNHE3(3,3),
     1   QIAMM
C COTRANSPORTER INFORMATION
	INTEGER  
     1   NMI,ORDMI(9),IMI(9,9),JMI(9,9),MMI(9,9),
     1   NIS,ORDIS(9),IIS(9,9),JIS(9,9),MIS(9,9)
	DOUBLE PRECISION RMI(9),RIS(9)
C
C   SUBSCRIPTS (CELL TYPE,SOLUTE,TIME)
C     INDICES FOR THE CELL TYPE: 1=PRINCIPAL; 2=ALPHA; 3=BETA
C
C       Z-      VALENCE OF I'TH SOLUTE
C       ZIMP-   VALENCE OF IMPERMEANT SPECIES
C       RT-     GAS CONST. TIMES TEMP  (MMHG.ML/MMOL)
C       RTE-    GAS CONST. TIMES TEMP  (JOULE/MMOL)
C       F-      FARADAY (COUL/MOL)
C
C       EPSI-   TOLERANCE FOR THE ERROR VECTOR
C       DT-     TIME STEP
C       RTAU-   REAL REPRESENTATION OF THE INTEGER TAU
C	TIME-	ELAPSED TIME OF THE SIMULATED EXPT.
C
C       L-      CELL HEIGHT
C       L0-     CELL MEMBRANE HEIGHT
C       MUA-    CHANNEL AREA COMPLIANCE
C       MUV-    CHANNEL VOLUME COMPLIANCE
C
C       AME-    AREA OF TIGHT JUNCTION
C       AE-     CHANNEL AREA AT BASE
C       AMI-    LUMINAL CELL AREA
C       AIE-    LATERAL CELL AREA
C       AIS-    BASAL CELL AREA
C
C       CHVL-   CHANNEL VOLUME
C       CLVL-   CELL VOLUME
C       CHVL0-  REFERENCE CELL VOLUME
C       CLVL0-  REFERENCE CELL VOLUME
C       IMP0-  CELL IMPERMEANT SPECIES CONC. AT REFERENCE VOLUME
C       IMP-    CELL IMPERMEANT SPECIES CONC. AT CLVL
C
C	CBUF-	CELL BUFFER CONCENTRATION
C	HCBUF-	PROTONATED CELL BUFFER
C	TBUF-	TOTAL CELL BUFFER CONCENTRATION FOR THE REFERENCE VOLUME
C	PK.-	.=B,C,P  FOR PK OF CELL BUFFERS, HCO3, OR HPO4
C	KHY.,KDHY.-  .=P,A,B,E HYDRATION/DEHYDRATION CONSTANT FOR CO2
C
C       LP..-   WATER PERMEABILITY (CM/SEC.MMHG)
C       S..-    REFLECTION COEFFICIENT
C       H..-    SOLUTE PERMEABILITY (CM/SEC)
C       C..-    MEAN SOLUTE CONCENTRATION
C	        ME-     TIGHT JUNCTION
C               MI-     CELL MUCOSA
C               IE-     LATERAL CELL
C               ES-     CHANNEL BASEMENT MEMBRANE
C               IS-     CELL SEROSA
C
C       LM,LS-  MATRICES OF ONSAGER COEFFICIENTS FOR MUCOSAL
C               AND BASOLATERAL CELL MEMBRANES
C
C       V.-     VOLTAGE (MVOLT)
C       P.-     PRESSURE (MMHG)
C       C.-     CONCENTRATION (MMOL/ML)
C       X.-     ELECTROCHEMICAL POTENTIAL
C	LCH.-	LOG CONCENTRATION HYDROGEN (PH)
C               . = M,S,I,E
C
C       IMP.-   IMPERMEANT SPECIES CONCENTRATION
C		. = M,S
C       DUMVM-  DUMMY VARIABLE HOLDING VM (OPEN CIRCUIT)
C
C       NP-     NA+ EXTRUSION OUT THE LATERAL CELL MEMBRANE
C	KNPN-	NA AFFINITY FOR THE PUMP
C	KNPK-	K AFFINITY FOR THE PUMP
C       KNH4-   RELATIVE AFFINITY FOR NH4+ FOR THE PUMP
C	ATMI-	ACTIVE TRANSPORT ACROSS THE LUMINAL MEMBRANE
C	ATIS-	ACTIVE TRANSPORT ACROSS THE PERITUBULAR MEMBRANE
C	NPHK-	ABUNDANCE OF H-K-ATPASE
C	NAE1-	ABUNDANCE OF AE1
C	NTSC-	ABUNDANCE OF TSC
C	NNHE3-	ABUNDANCE OF NHE3
C
C	JNAK-	FLUX (MMOL/S) ACROSS THE PERITUBULAR NA,K-ATPASE
C	JHK-	FLUX ACROSS THE LUMINAL H,K-ATPASE
C	JHP-	FLUX ACROSS THE H-ATPASE
C	JAE1-	FLUX ACROSS AE1
C	JTSC-	FLUX ACROSS TSC
C	JNHE3-	FLUX ACROSS NHE3
C       QIAMM-  CYTOSOLIC PRODUCTION OF NH3
C
C       FEVM-   JUCTION VOLUME FLOW (ML/SEC)
C       FEKM-   JUNCTION SOLUTE FLUX (MMOL/SEC)
C       FIVM-   CELL APICAL VOLUME FLOW
C       FIKM-   CELL APICAL SOLUTE FLUX
C       FEVS-   CHANNEL BASEMENT MEMBRANE VOLUME FLOW
C       FEKS-   CHANNEL BASEMENT MEMBRANE SOLUTE FLUX
C       FIVS-   BASAL CELL VOLUME FLOW
C       FIKS-   BASAL CELL SOLUTE FLUX
C       CURE-   CHANNEL CURRENT (MAMP)
C       CURI-   CELL CURRENT
C       JV-     LATERAL CELL MEMBRANE VOLUME FLOW
C       JS-     LATERAL CELL MEMBRANE SOLUTE FLUX
C
C CONTROL VARIABLES
	DOUBLE PRECISION 
     1   HCON(10),HCON0(10),DHCON(10,4),CON(4),CON0(10,4)
C
C	HCON-	VALUE OF (2) CONTROLLED VARIABLES
C 	HCON0-	BASELINE VALUE OF CONTROLLED VARIABLE
C	DHCON-	SENSITIVITY OF CONTROLLED VARIABLE TO ONE OF (3) CONTROLLERS
C	CON-	VALUE OF CONTROLLING VARIABLE
C	CON0-	SET POINT OF CONROLLING VARIABLE
C
C CONTROLLED VARIABLES ARE 
C	(1) LUMINAL NA/H EXCHANGE
C	(2) LUMINAL NA-GLUC COTRANSPORT
C	(3) LUMINAL CL/HCO3 EXCHANGE
C	(4) LUMINAL CL/HCO2 EXCHANGE
C	(5) PERITUBULAR K CHANNEL
C	(6) PERITUBULAR CL CHANNEL
C	(7) PERITUBULAR KCL COTRANSPORT
C	(8) PERITUBULAR NA-3HCO3 COTRANSPORT
C	(9) PERITUBULAR NA-2HCO3/CL
C	(10) ACTIVE NA TRANSPORT
C CONTROLLING VARIABLES ARE 
C	(1) CELL VOLUME 
C	(2) CELL HCO3 
C	(3) ACTIVE NA TRANSPORT
C	(4) CELL NA (A CA SURROGATE)
C
C CONTROL EQUATIONS ARE OF THE FORM 
C   HCON(I) = HCON0(I) (1 + DHCON(I,J) (CON(J)/CON0(I,J) -1)
C
C
	DOUBLE PRECISION GAMMA(60)
C
        COMMON SOLS,T,TAU,TLIM,
     1   Z,RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,
     1   PKC,PKF,PKN,PKP,KHY,KDHY,L0,L,
     1   DUMVM,VM,PM,CM,IMPM,LCHM,XM,
     1   VS,PS,CS,IMPS,LCHS,XS,
     1   AME,AE0,AE,MUA,CHVL0,CHVL,MUV,
     1   LPME,LPES,SME,SES,
     1   HME,HES,CME,CES,
     1   VE,PE,CE,LCHE,XE,
     1   FEVM,FEKM,FEVS,FEKS,CURE,
     1   AIE,AMI,AIS,CLVL0,IMP0,CLVL,
     1   ZIMP,TBUF,PKB,CBUF,HCBUF,
     1   LPMI,LPIS,SMI,SIS,
     1   HMI,HIS,CMI,CIE,CIS,
     1   LMI,LIS,ATMI,ATIS,
     1   VI,PI,CI,IMP,LCHI,XI,
     1   FIVM,FIKM,FIVS,FIKS,CURI,
     1   JV,JK
	COMMON/KINET/
     1   NP,KNPN,KNPK,KNH4,NPHK,
     1   LHP,XIHP,XHP,NAE1,NTSC,NNHE3,
     1   JNAK,JHK,JHP,JAE1,JTSC,JNHE3,QIAMM
	COMMON/COTR/
     1   NMI,ORDMI,IMI,JMI,MMI,
     1   NIS,ORDIS,IIS,JIS,MIS,RMI,RIS,
     1   HCON,HCON0,DHCON,CON,CON0
C
C SW1--IF ON (1) GIVES OPEN CIRCUIT CONDITIONS (VM IS A VARIABLE
C IN THE GAMMA ARRAY); IF OFF (0) READS VM.
C
C
	OPEN (20,FILE='param.dat')
	OPEN (21,FILE='result.dat')
	OPEN (22,FILE='bound.dat')
	OPEN (23,FILE='xy.dat')
	OPEN (24,FILE='guess.dat')
	OPEN (26,FILE='ptim.dat')
	OPEN (27,FILE='fluxes.dat')
C
C EXPERIMENTS WILL CYCLE FROM THIS POINT.
C
C  SOLUTE INDEX:
C	1-	NA+
C	2-	K+
C	3-	CL-
C	4-	HCO3-
C	5-	H2CO3
C	6-	CO2
C	7-	HPO4--
C	8-	H2PO4-
C	9-	UREA
C	10-	NH3
C	11-	NH4+
C	12-	H+
C	13-	HCO2-
C	14-	H2CO2
C	15-	GLUCOSE
C
C
	SOLS=15
	PRINT 20
   20   FORMAT (' TYPE 1/0 FOR OPEN/CLOSED CIRCUIT:  ',$)
	READ *,SW1
	NN=2*SOLS+4+SW1
C
	READ (24,24) (GAMMA(J),J=1,NN)
   24   FORMAT (D30.20)
C
	PRINT 27
   27   FORMAT(' NUMBER OF EXPERIMENTS = ',$)
	READ *, EXP
	DO 600 COUNT=1,EXP
C
	CALL PSET(1)
	T=1
	CALL GAMSET(1,GAMMA)
C
C
C BOUNDARY VALUES,ACTIVE TRANSPORT,AND THE TIME INCREMENT ARE
C READ. THE TIME STEP WILL CYCLE FROM THIS POINT.
C
	T=0
	TIME=0.D0
   40   T=T+1
	READ(22,50) DT,DUMVM,PM,IMPM,VS,PS,IMPS,
     1  (CM(I),CS(I),I=1,11),(CM(I),CS(I),I=13,SOLS)
   50   FORMAT (D12.4,/,6F8.4,/,(2F14.9))
	LCHM=PKC + DLOG10(CM(4)/CM(5))
	LCHS=PKC + DLOG10(CS(4)/CS(5))
	CM(12)=10.**(-LCHM)
	CS(12)=10.**(-LCHS)
	IF (SW1.EQ.0) VM=DUMVM
	TIME=TIME+DT*FLOAT(T-1)
	RTAU=DBLE(FLOAT(TAU))/DT
	IF (TAU.EQ.1) CALL PTIM(NPAR)
C
	HCON0(1)=NNHE3(1)
	HCON0(2)=RMI(4)
	HCON0(3)=RMI(1)
	HCON0(4)=RMI(2)
	HCON0(5)=HIS(1,2)
	HCON0(6)=HIS(1,3)
	HCON0(7)=RIS(1)
	HCON0(8)=RIS(2)
	HCON0(9)=RIS(3)
	HCON0(10)=NP(1)
C
C   START THE SPATIAL ITERATION.
C
	PRINT 72, COUNT,TIME
   72   FORMAT(5X,'EXP=',I3,5X,'TIME=',F8.3)
C
	CALL NEWTON(NN,GAMMA)
C
C THE SPATIAL ITERATION CONVERGED,RESULTS WERE OUTPUT, AND
C THE PROGRAM PICKS UP AT THIS POINT:
C
  558   IF(T.EQ.1) CALL ARESUL
	CALL BRESUL
	CALL CRESUL
	CALL FRESUL
	IF (TAU.EQ.1) GO TO 570
C
C  THUS IN THE STEADY STATE CASE WE ARE DONE.
	PRINT 560
  560   FORMAT (' PROBLEM SOLVED FOR THE STEADY STATE')
	GO TO 600
C
C  FOR THE TRANSIENT EXPERIMENT,THE VARIABLES AT T=1 ARE SET;
C  IF NEEDED, THE GUESS FOR T=2 IS GENERATED.
C
  570   CALL RESET
C
C  IF TIME IS UP,STOP.
  585   TLIM=TLIM-1
	IF (TLIM.GT.0) GO TO 40
	PRINT 590
  590   FORMAT (' TIME IS UP')
C
C
  600   CONTINUE
C
	CLOSE (24,STATUS='KEEP')
	OPEN (24,FILE='guess.new')
	WRITE (24,624) (GAMMA(J),J=1,NN)
  624   FORMAT(D30.20)
	STOP
	END
