	SUBROUTINE ERRVEC(PHI)
C
	INTEGER SOLS,T
C
C GENERAL PARAMETERS
	DOUBLE PRECISION
     1   Z(15),RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,L0,L(2),
     1   PKC,PKF,PKN,PKP,KHY(4),KDHY(4)
C LUMINAL AND PERITUBULAR PARAMETERS
	DOUBLE PRECISION
     1   DUMVM,VM,PM,CM(15),IMPM,LCHM,XM(15),
     1   VS,PS,CS(15),IMPS,LCHS,XS(15)
C INTERSPACE PARAMETERS
	DOUBLE PRECISION
     1   AME,AE0,AE(2),MUA,CHVL0,CHVL(2),MUV,
     1   LPME,LPES,SME(15),SES(15),
     1   HME(15),HES(15),CME(15),CES(15),
     1   VE(2),PE(2),CE(15,2),LCHE,XE(15),
     1   FEVM(2),FEKM(15,2),FEVS(2),FEKS(15,2),CURE
C CELL PARAMETERS
	DOUBLE PRECISION
     1   AIE(3),AMI(3),AIS(3),CLVL0(3),IMP0(3),CLVL(3,2),
     1   ZIMP(3),TBUF(3),PKB(3),CBUF(3,2),HCBUF(3,2),
     1   LPMI(3),LPIS(3),SMI(3,15),SIS(3,15),
     1   HMI(3,15),HIS(3,15),CMI(3,15),CIE(3,15),CIS(3,15),
     1   LMI(3,15,15),LIS(3,15,15),ATMI(3,15),ATIS(3,15),
     1   VI(3,2),PI(3,2),CI(3,15,2),IMP(3),LCHI(3),XI(3,15),
     1   FIVM(3,2),FIKM(3,15,2),FIVS(3,2),FIKS(3,15,2),CURI(3),
     1   JV(3,2),JK(3,15,2)
C SPECIAL TRANSPORTERS
	DOUBLE PRECISION
     1   NP(3),KNPN(3),KNPK(3),KNH4(3),NPHK(3),
     1   LHP(3),XIHP(3),XHP(3),NAE1(3),NTSC(3),NNHE3(3),
     1   JNAK(3,3),JHK(3),JHP(3),JAE1(3),JTSC(3),JNHE3(3,3),
     1   QIAMM
C
	DOUBLE PRECISION PHI(60),VHK,SHK(4),
     1   VNHE3(3),SNHE3(3,2),QE(15),QI(3,15),
     1   ZME(15),ZMI(3,15),ZIE(3,15),ZES(15),ZIS(3,15)
C
C
        COMMON SOLS,T,
     1   Z,RT,RTE,F,
     1   EPSI,DT,RTAU,TIME,
     1   PKC,PKF,PKN,PKP,KHY,KDHY,L0,L,
     1   DUMVM,VM,PM,CM,IMPM,LCHM,XM,
     1   VS,PS,CS,IMPS,LCHS,XS,
     1   AME,AE0,AE,MUA,CHVL0,CHVL,MUV,
     1   LPME,LPES,SME,SES,
     1   HME,HES,CME,CES,
     1   VE,PE,CE,LCHE,XE,
     1   FEVM,FEKM,FEVS,FEKS,CURE,
     1   AIE,AMI,AIS,CLVL0,IMP0,CLVL,
     1   ZIMP,TBUF,PKB,CBUF,HCBUF,
     1   LPMI,LPIS,SMI,SIS,
     1   HMI,HIS,CMI,CIE,CIS,
     1   LMI,LIS,ATMI,ATIS,
     1   VI,PI,CI,IMP,LCHI,XI,
     1   FIVM,FIKM,FIVS,FIKS,CURI,
     1   JV,JK
	COMMON/KINET/
     1   NP,KNPN,KNPK,KNH4,NPHK,
     1   LHP,XIHP,XHP,NAE1,NTSC,NNHE3,
     1   JNAK,JHK,JHP,JAE1,JTSC,JNHE3,QIAMM
C
C
C COMPUTE THE RELEVANT VOLUMES
C
	AE(T)=AE0*(1.0+MUA*(PE(T)-PM))
	IF((AE(T).LT.AE0)) AE(T)=AE0
	CHVL(T)=CHVL0*(1.0+MUV*(PE(T)-PM))
	IF((CHVL(T).LT.CHVL0)) CHVL(T)=CHVL0
	L(T)=CHVL(T)
	LCHE=PKC+DLOG10(CE(4,T)/CE(5,T))
	CE(12,T)=10**(-LCHE)
C
	DO 20 K=1,1
	CLVL(K,T)=CLVL0(K)*IMP0(K)/IMP(K)
	L(T)=L(T)+CLVL(K,T)
	PI(K,T)=PM
	LCHI(K)=PKC+DLOG10(CI(K,4,T)/CI(K,5,T))
	CI(K,12,T)=10**(-LCHI(K))
   20   CONTINUE
C
C COMPUTE THE MEMBRANE FLUXES
C
	FEVM(T)=AME*LPME*(PM-PE(T)-RT*IMPM)/RT
	FEVS(T)=AE(T)*LPES*(RT*IMPS+PE(T)-PS)/RT
	DO 30 K=1,1
	FIVM(K,T)=AMI(K)*LPMI(K)*(RT*IMP(K)-RT*IMPM+PM-PI(K,T))/RT
	FIVS(K,T)=AIS(K)*LPIS(K)*(PI(K,T)-PS+RT*IMPS-RT*IMP(K))/RT
   30   JV(K,T)=LPIS(K)*AIE(K)*(PI(K,T)-PE(T)-RT*IMP(K))/RT
	DO 35 I=1,SOLS
	FEVM(T)=FEVM(T)+AME*LPME*SME(I)*(CE(I,T)-CM(I))
	FEVS(T)=FEVS(T)+AE(T)*LPES*SES(I)*(CS(I)-CE(I,T))
	DO 35 K=1,1
	FIVM(K,T)=FIVM(K,T)+AMI(K)*LPMI(K)*SMI(K,I)*(CI(K,I,T)-CM(I))
	FIVS(K,T)=FIVS(K,T)+AIS(K)*LPIS(K)*SIS(K,I)*(CS(I)-CI(K,I,T))
	JV(K,T)=JV(K,T)+LPIS(K)*AIE(K)*SIS(K,I)*(CE(I,T)-CI(K,I,T))
   35   CONTINUE
C
	DO 100 I=1,SOLS
C
	CME(I)=(CE(I,T)-CM(I))
	IF (CME(I)) 347,346,347
  346   CME(I)=CM(I)
	GO TO 348
  347   CME(I)=CME(I)/DLOG(CE(I,T)/CM(I))
  348   CES(I)=(CE(I,T)-CS(I))
	IF (CES(I)) 350,349,350
  349   CES(I)=CS(I)
	GO TO 351
  350   CES(I)=CES(I)/DLOG(CE(I,T)/CS(I))
  351   DO 360 K=1,1
	CMI(K,I)=(CI(K,I,T)-CM(I))
	IF (CMI(K,I)) 353,352,353
  352   CMI(K,I)=CM(I)
	GO TO 354
  353   CMI(K,I)=CMI(K,I)/DLOG(CI(K,I,T)/CM(I))
  354   CIS(K,I)=(CI(K,I,T)-CS(I))
	IF (CIS(K,I)) 356,355,356
  355   CIS(K,I)=CS(I)
	GO TO 357
  356   CIS(K,I)=CIS(K,I)/DLOG(CI(K,I,T)/CS(I))
  357   CIE(K,I)=(CI(K,I,T)-CE(I,T))
	IF (CIE(K,I)) 359,358,359
  358   CIE(K,I)=CI(K,I,T)
	GO TO 360
  359   CIE(K,I)=CIE(K,I)/DLOG(CI(K,I,T)/CE(I,T))
  360   CONTINUE
C
C
	XE(I)=RTE*DLOG(CE(I,T))+Z(I)*F*VE(T)*1.D-6
	XM(I)=RTE*DLOG(CM(I))+Z(I)*F*VM*1.D-6
	XS(I)=RTE*DLOG(CS(I))+Z(I)*F*VS*1.D-6
	DO 370 K=1,1
  370   XI(K,I)=RTE*DLOG(CI(K,I,T))+Z(I)*F*VI(K,T)*1.D-6
C
	ZME(I)=Z(I)*F*(VM-VE(T))*1.D-6/RTE
	ZES(I)=Z(I)*F*(VE(T)-VS)*1.D-6/RTE
	DO 380 K=1,1
	ZMI(K,I)=Z(I)*F*(VM-VI(K,T))*1.D-6/RTE
	ZIE(K,I)=Z(I)*F*(VI(K,T)-VE(T))*1.D-6/RTE
  380   ZIS(K,I)=Z(I)*F*(VI(K,T)-VS)*1.D-6/RTE
C
C CONVECTIVE FLUXES
C
	FEKM(I,T)=FEVM(T)*(1.D0-SME(I))*CME(I)
	FEKS(I,T)=FEVS(T)*(1.D0-SES(I))*CES(I)
	DO 390 K=1,1
	FIKM(K,I,T)=FIVM(K,T)*(1.D0-SMI(K,I))*CMI(K,I)
	FIKS(K,I,T)=FIVS(K,T)*(1.D0-SIS(K,I))*CIS(K,I)
  390   JK(K,I,T)=JV(K,T)*(1.D0-SIS(K,I))*CIE(K,I)
C
C GOLDMAN FLUXES
C
	IF (DABS(Z(I)).LT.0.1) GO TO 90
	FEKM(I,T)=FEKM(I,T)+HME(I)*AME*ZME(I)*
     1   (CM(I) - CE(I,T)*DEXP(-ZME(I)))/(1.D0 - DEXP(-ZME(I)))
	FEKS(I,T)=FEKS(I,T)+HES(I)*AE(T)*ZES(I)*
     1   (CE(I,T) - CS(I)*DEXP(-ZES(I)))/(1.D0 - DEXP(-ZES(I)))
	DO 85 K=1,1
	FIKM(K,I,T)=FIKM(K,I,T)+HMI(K,I)*AMI(K)*ZMI(K,I)*
     1   (CM(I) - CI(K,I,T)*DEXP(-ZMI(K,I)))/(1.D0 - DEXP(-ZMI(K,I)))
	JK(K,I,T)=JK(K,I,T)+HIS(K,I)*AIE(K)*ZIE(K,I)*
     1   (CI(K,I,T) - CE(I,T)*DEXP(-ZIE(K,I)))/(1.D0 - DEXP(-ZIE(K,I)))
   85   FIKS(K,I,T)=FIKS(K,I,T)+HIS(K,I)*AIS(K)*ZIS(K,I)*
     1   (CI(K,I,T) - CS(I)*DEXP(-ZIS(K,I)))/(1.D0 - DEXP(-ZIS(K,I)))
	GO TO 100
C
   90   FEKM(I,T)=FEKM(I,T)+HME(I)*AME*(CM(I) - CE(I,T))
	FEKS(I,T)=FEKS(I,T)+HES(I)*AE(T)*(CE(I,T) - CS(I))
	DO 95 K=1,1
	FIKM(K,I,T)=FIKM(K,I,T)+HMI(K,I)*AMI(K)*(CM(I) - CI(K,I,T))
	JK(K,I,T)=JK(K,I,T)+HIS(K,I)*AIE(K)*(CI(K,I,T) - CE(I,T))
   95   FIKS(K,I,T)=FIKS(K,I,T)+HIS(K,I)*AIS(K)*(CI(K,I,T) - CS(I))
  100   CONTINUE
C
C NET COTRANSPORTERS
C
	DO 120 K=1,1
	DO 120 I=1,SOLS
	DO 120 J=1,SOLS
	FIKM(K,I,T)=FIKM(K,I,T)+LMI(K,I,J)*AMI(K)*(XM(J)-XI(K,J))
	FIKS(K,I,T)=FIKS(K,I,T)+LIS(K,I,J)*AIS(K)*(XI(K,J)-XS(J))
  120   JK(K,I,T)=JK(K,I,T)+LIS(K,I,J)*AIE(K)*(XI(K,J)-XE(J))
C
C THE NAH EXCHANGER
C   TRANSLATE CONCENTRATIONS TO THE NAH MODEL
C
	SNHE3(1,1)=CM(1)
	SNHE3(2,1)=CM(12)
	SNHE3(3,1)=CM(11)
	SNHE3(1,2)=CI(1,1,T)
	SNHE3(2,2)=CI(1,12,T)
	SNHE3(3,2)=CI(1,11,T)
C
	CALL NHE3(VNHE3,SNHE3)
C
	JNHE3(1,1)= NNHE3(1)*AMI(1)*VNHE3(1)
	JNHE3(1,2)= NNHE3(1)*AMI(1)*VNHE3(2)
	JNHE3(1,3)= NNHE3(1)*AMI(1)*VNHE3(3)
C
	FIKM(1,1,T)=FIKM(1,1,T) + JNHE3(1,1)
	FIKM(1,12,T)=FIKM(1,12,T) + JNHE3(1,2)
	FIKM(1,11,T)=FIKM(1,11,T) + JNHE3(1,3)
C
C PUMPS
C
C  SODIUM PUMPS
C
	DO 125 K=1,1
        KNPN(K)=0.0002*(1.D0+CI(K,2,T)/.00833)
        KNPK(K)=0.0001*(1.D0+CS(1)/.0185)
        ATIS(K,1)=NP(K)*(CI(K,1,T)/(KNPN(K)+CI(K,1,T)))**3*
     1    (CS(2)/(KNPK(K)+CS(2)))**2
C  ALLOW FOR COMPETITION BETWEEN K+ AND NH4+ 
	ATIS(K,2)=-ATIS(K,1)*0.667*CE(2,T)/(CE(2,T)+CE(11,T)/KNH4(K))
	ATIS(K,11)=-ATIS(K,1)*0.667*CE(11,T)/(KNH4(K)*CE(2,T)+CE(11,T))
C
	JK(K,1,T)=JK(K,1,T)+AIE(K)*ATIS(K,1)
	JK(K,2,T)=JK(K,2,T)+AIE(K)*ATIS(K,2)
	JK(K,11,T)=JK(K,11,T)+AIE(K)*ATIS(K,11)
	FIKS(K,1,T)=FIKS(K,1,T)+AIS(K)*ATIS(K,1)
	FIKS(K,2,T)=FIKS(K,2,T)+AIS(K)*ATIS(K,2)
	FIKS(K,11,T)=FIKS(K,11,T)+AIS(K)*ATIS(K,11)
C
	JNAK(K,1)= AIE(K)*ATIS(K,1)+AIS(K)*ATIS(K,1)
	JNAK(K,2)= AIE(K)*ATIS(K,2)+AIS(K)*ATIS(K,2)
	JNAK(K,3)= AIE(K)*ATIS(K,11)+AIS(K)*ATIS(K,11)
  125   CONTINUE
C
C  PROTON PUMPS
C
 	ATMI(1,12)=
     1   -LHP(1)/(1.+EXP(XIHP(1)*(XM(12)-XI(1,12)-XHP(1))))
	JHP(1)= -AMI(1)*LHP(1)/(1.+EXP(XIHP(1)*(XM(12)-XI(1,12)-XHP(1))))
	FIKM(1,12,T)=FIKM(1,12,T)+AMI(1)*ATMI(1,12)
C
C
C ESTABLISH THE ERROR VECTORS, THE "PHI" ARRAY.
C
C FIRST FOR THE INTERSPACE
C   ELECTRONEUTRALITY
	PHI(1)=0.D0
	DO 130 I=1,SOLS
  130   PHI(1)=PHI(1)+Z(I)*CE(I,T)
C
	IF (T.EQ.1) GO TO 140
C
C   MASS CONSERVATION IN THE TIME-DEPENDENT CASE
	PHI(2)=FEVS(T)-FEVM(T)
     1   +RTAU*(CHVL(T)-CHVL(T-1))
	DO 135 I=1,SOLS
	QE(I)=FEKS(I,T)-FEKM(I,T)
     1    +RTAU*(CE(I,T)*CHVL(T)-CE(I,T-1)*CHVL(T-1))
  135   CONTINUE
	DO 138 K=1,1
	PHI(2)=PHI(2) -JV(K,T)
	DO 136 I=1,SOLS
	QE(I)=QE(I) -JK(K,I,T)
  136   CONTINUE
  138   CONTINUE
	GO TO 149
C
C   MASS CONSERVATION IN THE STEADY-STATE CASE
  140   PHI(2)=FEVS(T)-FEVM(T)
	DO 145 I=1,SOLS
  145   QE(I)=FEKS(I,T)-FEKM(I,T)
	DO 148 K=1,1
	PHI(2)=PHI(2)-JV(K,T)
	DO 146 I=1,SOLS
  146   QE(I)=QE(I)-JK(K,I,T)
  148   CONTINUE
  149   CONTINUE
C
	DO 200 K=1,1
	KK=SOLS+1
C
C THEN FOR THE CELLS
C   ELECTRONEUTRALITY
	PHI(1+KK)=IMP(K)*ZIMP(K)-CBUF(K,T)
	DO 150 I=1,SOLS
  150   PHI(1+KK)=PHI(1+KK)+Z(I)*CI(K,I,T)
C
	IF (T.EQ.1) GO TO 160
C
C   MASS CONSERVATION IN THE TIME-DEPENDENT CASE
	PHI(2+KK)=FIVS(K,T)-FIVM(K,T)
     1  +JV(K,T)+RTAU*(CLVL(K,T)-CLVL(K,T-1))
	DO 155 I=1,SOLS
	QI(K,I)=FIKS(K,I,T)-
     1   FIKM(K,I,T)+JK(K,I,T)+
     1     RTAU*(CI(K,I,T)*CLVL(K,T)-CI(K,I,T-1)*CLVL(K,T-1))
  155   CONTINUE
C
C   THE PROTON FLUX MUST INCLUDE THE CELLULAR BUFFERS
C
	QI(K,12)=QI(K,12) -
     1    RTAU*(CBUF(K,T)*CLVL(K,T)-CBUF(K,T-1)*CLVL(K,T-1))
C
	GO TO 169
C
C   MASS CONSERVATION IN THE STEADY-STATE CASE
  160   PHI(2+KK)=FIVS(K,T)-FIVM(K,T)+JV(K,T)
	DO 165 I=1,SOLS
  165   QI(K,I)=FIKS(K,I,T)-FIKM(K,I,T)+JK(K,I,T)
  169   CONTINUE
C
C
  200   CONTINUE
C
	PHI(2)=1.D6*PHI(2)
	DO 205 I=1,SOLS
  205   QE(I)=1.D6*QE(I)
	PHI(SOLS+3)=1.D6*PHI(SOLS+3)
	DO 210 K=1,1
	DO 210 I=1,SOLS
  210   QI(K,I)=1.D6*QI(K,I)
C
C  SOLUTE INDEX:
C	1-	NA+
C	2-	K+
C	3-	CL-
C	4-	HCO3-
C	5-	H2CO3
C	6-	CO2
C	7-	HPO4--
C	8-	H2PO4-
C	9-	UREA
C	10-	NH3
C	11-	NH4+
C	12-	H+
C	13-	HCO2-
C	14-	H2CO2
C	15-	GLUCOSE
C
C NOW SET THE PHIS IN TERMS OF THE SOLUTE GENERATION RATES
C   FIRST THE NON-REACTIVE SPECIES
C
	PHI(3)=QE(1)
	PHI(4)=QE(2)
	PHI(5)=QE(3)
	PHI(11)=QE(9)
	PHI(16)=QE(15)
C
	DO 215 K=1,1
	KK=SOLS+1
	PHI(3+KK)=QI(K,1)
	PHI(4+KK)=QI(K,2)
	PHI(5+KK)=QI(K,3)
	PHI(11+KK)=QI(K,9)
  215   PHI(16+KK)=QI(K,15)
C
C   CO2, FORMATE, PHOSPHATE, AND AMMONIA CONTENT:
C
	PHI(8)=QE(4)+QE(5)+QE(6)
	PHI(9)=QE(7)+QE(8)
	PHI(12)=QE(10)+QE(11)
	PHI(14)=QE(13)+QE(14)
	DO 230 K=1,1
	KK=SOLS+1
	PHI(KK+8)=QI(K,4)+QI(K,5)+QI(K,6)
	PHI(KK+9)=QI(K,7)+QI(K,8)
	PHI(KK+12)=QI(K,10)+QI(K,11) - 1.D6*QIAMM
	PHI(KK+14)=QI(K,13)+QI(K,14)
  230   CONTINUE
C
C   FORMATE, PHOSPHATE, AND AMMMONIA PH EQUILIBRIUM: 
C
	PHI(10)=LCHE-PKP-DLOG10(CE(7,T)/CE(8,T))
	PHI(13)=LCHE-PKN-DLOG10(CE(10,T)/CE(11,T))
	PHI(15)=LCHE-PKF-DLOG10(CE(13,T)/CE(14,T))
	DO 240 K=1,1
	KK=SOLS+1
	PHI(10+KK)=LCHI(K)-PKP-DLOG10(CI(K,7,T)/CI(K,8,T))
	PHI(13+KK)=LCHI(K)-PKN-DLOG10(CI(K,10,T)/CI(K,11,T))
	PHI(15+KK)=LCHI(K)-PKC-DLOG10(CI(K,13,T)/CI(K,14,T))
  240   CONTINUE
C
C   HYDRATION AND DHYDRATION OF CO2
C
	IF (T.EQ.1) GO TO 251
C
	PHI(7)=QE(6)+
     1   1.D6*CHVL(T)*(KHY(4)*CE(6,T)-KDHY(4)*CE(5,T))
	DO 250 K=1,1
	KK=SOLS+1
  250   PHI(7+KK)=QI(K,6)+
     1   1.D6*CLVL(K,T)*(KHY(K)*CI(K,6,T)-KDHY(K)*CI(K,5,T))
	GO TO 253
C
  251   PHI(7)=QE(6)+
     1   1.D6*CHVL(T)*(KHY(4)*CE(6,T)-KDHY(4)*CE(5,T))
	DO 252 K=1,1
	KK=SOLS+1
  252   PHI(7+KK)=QI(K,6)+
     1   1.D6*CLVL(K,T)*(KHY(K)*CI(K,6,T)-KDHY(K)*CI(K,5,T))
  253   CONTINUE
C
C   THE ERROR TERM FOR BICARBONATE GENERATION IS REPLACED BY CONSERVATION
C     OF CHARGE IN THE BUFFER REACTIONS.
C
	PHI(6)= -QE(4)+QE(8)+QE(11)+QE(12)+QE(14)
	DO 220 K=1,1
	KK=SOLS+1
  220   PHI(6+KK)= -QI(K,4)+QI(K,8)+QI(K,11)+QI(K,12)+QI(K,14)
C
C CELL BUFFER CONTENT AND PH EQUILIBRIUM
C
	DO 270 K=1,1
	PHI(3+2*SOLS)=CBUF(K,T)+HCBUF(K,T)-(TBUF(K)*CLVL0(K)/CLVL(K,T))
	PHI(4+2*SOLS)=PKB(K)-PKP
     1      -DLOG10((CI(K,7,T)*HCBUF(K,T))/(CI(K,8,T)*CBUF(K,T)))
  270   CONTINUE
C
C
C   THE OPEN CIRCUIT CONDITION
	CURE=0.D0
	DO 255 I=1,SOLS
  255   CURE=CURE+F*Z(I)*FEKM(I,T)
	PHI(5+2*SOLS)=CURE
	DO 265 K=1,1
 	CURI(K)=0.D0
	DO 260 I=1,SOLS
  260   CURI(K)=CURI(K)+F*Z(I)*FIKM(K,I,T)
  265   PHI(5+2*SOLS)=PHI(5+2*SOLS)+CURI(K)
	RETURN
	END
